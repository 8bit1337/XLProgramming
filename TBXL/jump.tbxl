100 REM ***** INIT *****
110 GRAPHICS 5
120 DIM M$(42):MH=7:MW=6
125 M$="DDAADDDDAADDDBBBBDBDBBDBDDCCDDDCDDCDCCDDCC"
130 POKE 752,1
140 X=16:EXEC DRAW_BG
150 X=55:EXEC DRAW_BG
160 REM ***** RESET GAME *****
170 POKE 656,1:POKE 657,9
180 PRINT "PRESS BUTTON TO START"
190 WHILE STRIG(0)<>0:WEND 
200 REM ***** START GAME *****
210 JUMP=0:Y=-7:SIGN=1:SCORE=0:DLAY=0
220 CLS :POKE 656,2:POKE 657,14
230 PRINT "SCORE:        "
240 WHILE 1
250   C=1:EXEC DRAW_ROPE
260   C=0:EXEC DRAW_ROPE
270   Y=Y+SIGN
280   POKE 656,2:POKE 657,23
290   PRINT SCORE
300   IF Y MOD 2=0
310     SOUND 0,120+5*Y,10,5
320   ENDIF 
330   C=2:EXEC DRAW_PLAYER
340   IF STRIG(0)=0 AND DLAY<=0
350     DLAY=5
360     C=0:EXEC DRAW_PLAYER
370     JUMP=-2
380     C=2:EXEC DRAW_PLAYER
390     C=0:EXEC DRAW_PLAYER
400     JUMP=0
410   ENDIF 
420   IF ABS(Y)>6
430     Y=6*SIGN
440     SIGN=-1*SIGN
450   ENDIF 
460   SOUND 0,0,0,0
470   LOCATE 38,35,Z
480   DLAY=DLAY-1
490   IF Y=6 AND SIGN=-1
500     IF Z=0
510       SCORE=SCORE+1
520     ELSE 
530       GOTO 170
540     ENDIF 
550   ENDIF 
560 WEND 
570 POKE 752,0
580 END 
590 REM ------------
595 PROC DRAW_BG
600   FOR I=1 TO MH
610     POSITION X,I+29
620     PRINT #6;M$(MW*I-MW+1,MW*I)
630   NEXT I
640 ENDPROC 
650 REM ------------
655 PROC DRAW_ROPE
660   COLOR C
670   PLOT 22,32
680   DRAWTO 28,32+Y
690   DRAWTO 48,32+Y
700   DRAWTO 54,32
710 ENDPROC 
720 REM ------------
725 PROC DRAW_PLAYER
730   COLOR C
740   CIRCLE 38,36+JUMP,2
750   FILLTO 38,36+JUMP
760 ENDPROC 
