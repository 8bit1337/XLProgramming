10 DIM A$(64)
12 DIM W$(20)
15 Q=256
16 L=200
20 M=PEEK(106)
25 N=M-16
30 POKE 106,N
40 A=ADR(A$)
45 W=ADR(W$)
50 GRAPHICS 18
60 S=DPEEK(88)
70 R=(M-8)*Q
80 T=R+Q
90 X=A+16
100 O=PEEK(756)*Q
105 Y=X+8
110 POKE 756,N
120 N=N*Q
130 MOVE O,N,Q*4
140 A$="\70\70\71\7F\7F\71\70\70\0E\0E\8E\FE\FE\8E\0E\0E\00\FF\FF\FF\18\18\18\3C\3C\18\18\18\FF\FF\FF\00\00\00\03\03\03\03\03\E3\00\00\C0\C0\C0\C0\C0\C7\FD\1F\03\1F\FC\E0\00\00\BF\F8\C0\F8\3F\07\00\00"
200 Z=Y+18
210 MOVE A,N+432,64
220 A$="\05\06\05\07\09\02\0A\02\0D\06\0D\07\09\0B\0A\0B\D8\D9\D6\D7\D8\D9\D6\D7\68\7D\04\7C\91\04\00\28\50\70\83\04\84\97\04\A0\A0\50\7D\7D\7E\7E\7F\7F\80\80\6D\6D\59\59\45\45\31\31\83\86\A9\D1"
230 W$="\0B\80\7F\7E\7D\0E\6D\59\45\31\07\83\84\85\86\0D\A9\BD\D1\E5"
300 POSITION 0,0
305 PRINT #6;"score"
310 POSITION 1,1
315 PRINT #6;"0"
320 POSITION 15,0
325 PRINT #6;"bases"
330 POSITION 17,1
335 PRINT #6;"0"
340 POSITION 9,6
345 PRINT #6;"Z["
350 POSITION 9,7
355 PRINT #6;"\]"
360 MOVE S,R,Q
400 WHILE STRIG(0)=1
401 WEND
410 B=3
420 MOVE R,S,Q
430 C=25
432 D=0
434 P=0
440 POSITION 17,1
445 PRINT #6;B
450 GOSUB 900
460 DO
470   D=D+1
480   IF D=C
500     E=PEEK(S+128)+PEEK(S+109)+PEEK(S+131)+PEEK(S+169)
510     IF E<>0
520       SOUND 0,243,10,5
530       IF B=0
532         EXIT
534       ENDIF
540       B=B-1
550     ENDIF
560     MOVE S+40,T+20,L
600     FOR I=0 TO 5
610       E=Y+3*I
620       MOVE T+PEEK(E),S+PEEK(E+1),PEEK(E+2)
630     NEXT I
640     GOSUB 900
650     D=0
660     IF C<>1
662       C=C-1
664     ENDIF
670   ENDIF
680   K=STICK(0)
690   FOR I=0 TO 3
700     E=W+5*I
710     IF K=PEEK(E)
720       IF I MOD 2=0
722         F=20
724       ELSE
726         F=1
728       ENDIF
730       FOR J=1 TO 4
740         G=S+PEEK(E+J)
750         IF PEEK(G)<>0
760           SOUND 0,29,10,5
770           P=P+1
780           POKE G,0
800           POKE G+F,0
810           EXIT
820         ENDIF
830       NEXT J
840     ENDIF
850   NEXT I
860   POSITION 1,1
865   PRINT #6;P
870   POSITION 17,1
875   PRINT #6;B
880   SOUND
890 LOOP
892 SOUND
894 POSITION 17,1:?#6;B
896 WHILE STRIG(0)=1
868 WEND
899 END
900 E=RAND(4)
910 F=A+4*E
915 G=X+2*E
920 POSITION PEEK(F),PEEK(F+1)  
925 PRINT #6;CHR$(PEEK(G))
930 POSITION PEEK(F+2),PEEK(F+3)
935 PRINT #6;CHR$(PEEK(G+1))
940 RETURN


REM ************* SCREEN *************
REM        01234567890123456789
REM   0( 0)SCORE          BASES0  (0)
REM  20( 1) 0               0  20 (1)
REM  40( 2)         vw         40 (2)
REM  60(03)                    60 (3)
REM  80(04)                    80 (4)
REM 100(05)                    100(5)
REM 120(06)     x   z[   x     120(6)
REM 140(07)     y   \[   y     140(7)
REM 160(08)                    160(8)
REM 180(09)                    180(9)
REM 200(10)                    200(10)
REM 220(11)         vw         220(11)
REM        01234567890123456789
REM ************* SCREEN *************

REM ************ SCREEN T ************
REM        01234567890123456789
REM   0( 0)                    0  (0)
REM  20( 1)         vw         20 (1)
REM  40( 2)                    40 (2)
REM  60(03)                    60 (3)
REM  80(04)                    80 (4)
REM 100(05)     x   z[   x     100(5)
REM 120(06)     y   \[   y     120(6)
REM 140(07)                    140(7)
REM 160(08)                    160(8)
REM 180(09)                    180(9)
REM 200(10)         vw         200(10)
REM 220(11)                    220(11)
REM        01234567890123456789
REM ************ SCREEN T ************

REM *** VARIABLES ***
REM A$  = used to setup default X/Y positions of enemy ships and custom graphics
REM A   = address of A$
REM B   = number of bases remaining
REM C   = countdown timer (i.e. how many loops have we gone through)
REM D   = max value for countdown timer (i.e. how many loops before we move screen/add new enemy)
REM EFGH= temp variables
REM IJ  = loop variables
REM K   = position of Joystick 0
REM L   = 240 (length of screen in memory)
REM M   = top of memory address
REM N   = new top of memory address/first address of new character set
REM O   = first address of original character set
REM P   = current score/points
REM Q   = 256
REM R   = first address of Reset screen
REM S   = first address of main Screen (240 characters)
REM T   = first address of Temp screen
REM W   = address of the string I'm using to look for successfull hits of enemies
REM X   = address for graphics of new enemy ships (L1L2/U1U2/R1R2/D1D2)
REM Y   = memory address info for moving ships
REM Z   = used when player tries to shoot enemy ships

REM A$
REM  A LOCATIONS                                     X GRAPHICAL CHARACTER   Y MEMORY LOCATIONS FOR ANIMATION MOVES                Z WHERE TO LOOK WHEN SHOOTING
REM LEFT--------|TOP--------|RIGHT------|BOTTOM-----|LEFT-|TOP--|RIGHT|BTTM-|LEFT----|LEFT----|TOP-----|RIGHT---|RIGHT---|BOTTOM--|LEFT-------------------|TOP--------------------|RIGHT|BTTM-|
REM \05\06\05\07\09\02\0A\02\0D\06\0D\07\09\0B\0A\0B\D8\D9\D6\D7\D8\D9\D6\D7\68\7D\04\7C\91\04\00\28\50\70\83\04\84\97\04\A0\A0\50\7D\7D\7E\7E\7F\7F\80\80\6D\6D\59\59\45\45\31\31\83\86\A9\D1"

REM W$
REM \LEFT-------------|TOP--------------|RIGHT------------|BOTTOM-----------|
REM \0B\80\7F\7E\7D\01\0E\6D\59\45\31\14\07\83\84\85\86\01\0D\A9\BD\D1\E5\14"
REM    0                  6                 12