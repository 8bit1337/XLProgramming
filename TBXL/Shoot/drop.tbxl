REM raindrops keep falling in my coffee
REM (c) 2017 by Jason Gruetzmacher

REM set our graphics mode and establish memory addresses
REM - S=main screen
REM - R=reset screen
REM - T=animation screen
REM our two string arrays are to set character graphics and for our music
REM we will replace the x, y, and z characters with our coffee cup (xy) and our raindrop (z)
REM the music array characters represention in memory give us the integers we need to play the song
REM Q=256 just saves me space, as I use 256 a lot
000 DIM A$(24),U$(17)
010 A=ADR(A$):U=ADR(U$)
020 Q=256:E=17:F=18
100 A$="\1F\17\17\1F\17\1F\1F\0F\E0\FC\FC\EC\EC\F8\E0\C0\00\08\1C\3E\3E\3E\1C\00"
110 U$="\00\48\40\48\51\58\48\79\3C\35\3C\40\48\40\51\58\2F"

REM this next set of code is mostly standard memory related code, which you will see in lots of basic programs
REM of note:
REM - S = address of screen
REM - P = address for row where player sits
REM - R = will contain a copy of the screen, so I can reset game
REM - T = used to animate raindrops/coffee cup
200 M=PEEK(106)
210 N=M-16
220 POKE 106,N
230 GRAPHICS 18
240 S=DPEEK(88)
250 P=S+220
260 R=(M-8)*Q
270 T=R+Q
300 O=PEEK(756)*Q
310 POKE 756,N
320 N=N*Q
330 MOVE O,N,Q*4
340 MOVE A,N+448,24

REM draw the screen, then save it at address R
350 POSITION  0, 0:?#6;"drops 0"
400 POSITION  9,11:?#6;"\F8\F9"
410 MOVE S,R,Q

REM infinite loop, so that you can start a new game just by pushing button
REM with more lines of code available I'd make the Esc key or something stop execution and reset memory addresses
420 DO
REM   wait for user to press trigger to begin, then reset screen and variables
430   SOUND
440   WHILE STRIG(0)=1:WEND  
450   MOVE R,S,Q
REM   L is the target time.  900/60=15 seconds
460   L=TIME+900
REM   B helps me keep track of elapsed time. 
REM   X=horizonal position of player.  D=number of drops collected
500   X=8:D=0
REM   ***** main game loop start *****
510   REPEAT
REM     C = time remaining, in seconds
REM     K = +1/-1, representing a move right or left
REM     STICK(0):  11=left, 7=right
520     C=L-TIME
530     K=(9-STICK(0))/2
REM     move player
540     IF (K=1 AND X<17) OR (K=-1 AND X>0)
600       MOVE P+X,T,4
610       X=X+K
620       MOVE T,P+X,4
630     ENDIF
REM     only move/add raindrops every second
640     IF C MOD 7=0
REM       move rain drops
650       MOVE S,T,Q
660       MOVE T+20,S+40,180
REM       check for collision with player
700       FOR I=1 TO 2
710         IF PEEK(P-20+X+I)<>0
720           D=D+1
730           I=2
740         ENDIF
750       NEXT I
REM       add random new rain drops
760       POSITION 2+RAND(15),2:?#6;"\DA"  
800     ENDIF
REM     play the appropriate note from the song
810     SOUND 0,PEEK(U+(D MOD 16)),10,5
REM     update the score and time remaining
815     C=INT(C/60)
820     POSITION 6,0:?#6;D
830     IF C<10
          POSITION E,0:?#6;" "
          POSITION F,0
        ELSE
          POSITION E,0
        ENDIF
        ?#6;C
900   UNTIL C<0
910   POSITION E,0:?#6;"\E5\EE\E4"
REM   ***** main game loop end *****
920 LOOP


REM   X=58, Y=59, Z=5A.  (orange)
REM     Lowercase = 78,79,7A    (green)
REM     Inverse = D8,D9,DA      (blue) 
REM     Inverse Lowercase = F8,F9,FA   (red)

REM ************* SCREEN *************
REM        01234567890123456789
REM   0( 0)DROPS 0          30  0 ( 0)
REM  20( 1)                    20 ( 1)
REM  40( 2)                    40 ( 2)
REM  60(03)                    60 ( 3)
REM  80(04)                    80 ( 4)
REM 100(05)                    100( 5)
REM 120(06)                    120( 6)
REM 140(07)                    140( 7)
REM 160(08)                    160( 8)
REM 180(09)                    180( 9)
REM 200(10)                    200(10)
REM 220(11)         yz         220(11)
REM        01234567890123456789
REM ************* SCREEN *************

REM Raindrops keep falling for my head for piano
REM A A A B A G F A A C2 C C D C B A A B G F E4
REM 72 72 72 64 72 81 91 72 72 121 60 60 53 60 64 72 72 64 81 91 47
