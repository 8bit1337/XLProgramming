REM set our graphics mode and establish memory addresses
REM - S=main screen
REM - R=reset screen
REM - T=animation screen
000 DIM A$(24),U$(16)
005 A=ADR(A$):U=ADR(U$)
006 A$="\07\3F\7F\E3\E3\FF\7F\3F\E0\FC\FE\C7\C7\FF\FE\FC\00\08\1C\3E\3E\3E\1C\00"
007 U$="\48\40\48\51\58\48\79\3C\35\3C\40\48\40\51\58\2F"
015 Q=256
020 M=PEEK(106)
030 N=M-16
040 POKE 106,N
050 GRAPHICS 18
100 S=DPEEK(88)
110 P=S+220
120 R=(M-8)*Q
130 T=R+Q
140 O=PEEK(756)*Q
150 POKE 756,N
160 N=N*Q
200 MOVE O,N,Q*4
210 MOVE A,N+448,40

220 POSITION  0, 0:?#6;"drops 0"
230 POSITION  9,11:?#6;"xy"
240 MOVE S,R,Q
REM ***** main loop *****
300 DO
REM   wait for user to press trigger to begin, then reset screen
310   SOUND
320   WHILE STRIG(0)=1:WEND  
REM   reset screen, game variables
330   MOVE R,S,Q
340   L=TIME+900:D=0:Z=8
350   REPEAT
360     C=INT((L-TIME)/60)
REM     move player: 11=left, 7=right
400     K=(9-STICK(0))/2
410     IF (K=1 AND Z<17) OR (K=-1 AND Z>0)
420       MOVE P+Z,T,4
435       Z=Z+K
440       MOVE T,P+Z,4
450     ENDIF
REM     only repeat these actions every 5 times through the loop
500     IF C MOD 5=0
REM       move rain drops
510       MOVE S,T,Q
520       MOVE T+20,S+40,180
REM       check for head collision
530       FOR I=1 TO 2
540         IF PEEK(P-20+Z+I)<>0
550           D=D+1
560           I=2
600         ENDIF
610       NEXT I
REM       add random new rain drops
620       POSITION 2+RAND(15),2:?#6;"Z"  
640     ENDIF
REM     play next note in song
655     SOUND 0,PEEK(U+(D MOD 15)),10,5
REM     update the scoreboard
700     POSITION  6,0:?#6;D
710     POSITION 17,0:?#6;C
720     W=W-1
735   UNTIL C=0
740 LOOP

REM ************* SCREEN *************
REM        01234567890123456789
REM   0( 0)DROPS 0          30  0 ( 0)
REM  20( 1)                    20 ( 1)
REM  40( 2)                    40 ( 2)
REM  60(03)                    60 ( 3)
REM  80(04)                    80 ( 4)
REM 100(05)                    100( 5)
REM 120(06)                    120( 6)
REM 140(07)                    140( 7)
REM 160(08)                    160( 8)
REM 180(09)                    180( 9)
REM 200(10)                    200(10)
REM 220(11)         yz         220(11)
REM        01234567890123456789
REM ************* SCREEN *************

REM 97 notes
REM Ee | Aq. Ae Ae Be | 7
REM Cq. Ce Ce Be | E+q.  Bq    Ce | Ce Aq Aq A+e | A+q. A+q A+e | G+q. G+q A+e | E+h. | 36
REM E+q. A+q A+e | A+q.  A+q   Ae | Ce Cq Cq De  | E+e E+q Dq Ce| Ce Aq. Re E+e| 30
REM E+e Dq De Cq | Ae Eq Ee Ee Ge | Ah.          | Aq. Rq 24

REM A A A B A G F A A C- C C D C B A A B G F E+
REM 72 72 72 64 72 81 91 72 72 121 60 60 53 60 64 72 72 64 81 91 47
REM 
