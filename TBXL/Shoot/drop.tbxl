REM raindrops keep falling in my coffee
REM (c) 2017 by Jason Gruetzmacher

REM set our graphics mode and establish memory addresses
REM - S=main screen
REM - R=reset screen
REM - T=animation screen
REM our two string arrays are to set character graphics and for our music
REM we will replace the x, y, and z characters with our coffee cup (xy) and our raindrop (z)
REM the music array characters represention in memory give us the integers we need to play the song
REM Q=256 just saves me space, as I use 256 a lot
000 DIM A$(24),U$(42)
010 A=ADR(A$):U=ADR(U$)
020 Q=256:E=17:F=18
100 A$="\1F\17\17\1F\17\1F\1F\0F\E0\FC\FC\EC\EC\F8\E0\C0\00\08\1C\3E\3E\3E\1C\00"
REM 110 U$="\00\48\40\48\51\58\48\79\3C\35\3C\40\48\40\51\58\2F"
110 U$="\00\48\00\48\00\48\00\40\00\48\00\51\00\58\00\48\00\48\00\3C\00\3C\00\3C\00\35\00\3C\00\40\00\48\00\48\00\40\00\51\00\58\00\2F"

REM A A A B A G F A A C2 C2 C2 D C B A A B G F E4
REM 72 72 72 64 72 81 91 72 72 121 60 60 53 60 64 72 72 64 81 91 47

REM this next set of code is mostly standard memory related code, which you will see in lots of basic programs
REM of note:
REM - S = address of screen
REM - V = address for row where collisions can occur
REM - P = address for row where player sits
REM - R = will contain a copy of the screen, so I can reset game
REM - T = used to animate raindrops/coffee cup
200 M=PEEK(106)
210 N=M-16
220 POKE 106,N
230 GRAPHICS 18
240 S=DPEEK(88)
250 V=S+200
255 P=V+20
260 R=(M-8)*Q
270 T=R+Q
300 O=PEEK(756)*Q
310 POKE 756,N
320 N=N*Q
330 MOVE O,N,Q*4
340 MOVE A,N+448,24

REM draw the screen, then save it at address R
350 POSITION  0, 0:?#6;"drops 0"
400 POSITION  9,11:?#6;"\F8\F9"
410 MOVE S,R,Q

REM infinite loop, so that you can start a new game just by pushing button
REM with more lines of code available I'd make the Esc key or something stop execution and reset memory addresses
420 DO
REM   wait for user to press trigger to begin, then reset screen and variables
430   SOUND
440   WHILE STRIG(0)=1:WEND  
450   MOVE R,S,Q
REM   L is the target time.  900/60=15 seconds
460   L=TIME+900
REM   B helps me keep track of elapsed time. 
REM   X=horizonal position of player minus 1.  D=number of drops collected
500   X=8:D=0
REM   ***** main game loop start *****
510   REPEAT
REM     C = time remaining, in seconds
REM     K = +1/-1, representing a move right or left
REM     STICK(0):  11=left, 7=right
520     C=L-TIME

530     K=(9-STICK(0))/2
REM     move player
540     IF (K=1 AND X<17) OR (K=-1 AND X>0)
600       MOVE P+X,T,4
610       X=X+K
620       MOVE T,P+X,4
630     ENDIF
REM     check for collision with player
640     FOR I=1 TO 2
650       IF PEEK(V+X+I)<>0
655         POKE V+X+I,122
656         POKE V+X+I,0
660         D=D+1
680       ENDIF
690     NEXT I
REM     only move/add raindrops every second

REM       move rain drops
710       MOVE S,T,Q
720       MOVE T+20,S+40,180
REM       add random new rain drops
760       POSITION 2+RAND(15),2:?#6;"\DA"  

REM     play the appropriate note from the song
810     SOUND 0,PEEK(U+(D MOD 41)),10,5
REM     update the score and time remaining
815     C=INT(C/60)
820     POSITION 6,0:?#6;D
830     IF C<10
900       POSITION E,0:?#6;" "
910       POSITION F,0
920     ELSE
930       POSITION E,0
940     ENDIF
950     ?#6;C
960   UNTIL C<0
970   POSITION E,0:?#6;"\E5\EE\E4"
REM   ***** main game loop end *****
980 LOOP


FOR I=1 TO 2
  IF PEEK(V+X+I)<>0
    POKE V+X+I,122
    D=D+1
    I=2
  ENDIF
NEXT I


REM   X=58, Y=59, Z=5A.  (orange)
REM     Lowercase = 78,79,7A    (green)
REM     Inverse = D8,D9,DA      (blue) 
REM     Inverse Lowercase = F8,F9,FA   (red)

REM ************* SCREEN *************
REM        01234567890123456789
REM   0( 0)DROPS 0          30  0 ( 0)
REM  20( 1)                    20 ( 1)
REM  40( 2)                    40 ( 2)
REM  60(03)                    60 ( 3)
REM  80(04)                    80 ( 4)
REM 100(05)                    100( 5)
REM 120(06)                    120( 6)
REM 140(07)                    140( 7)
REM 160(08)                    160( 8)
REM 180(09)                    180( 9)
REM 200(10)                    200(10)
REM 220(11)         yz         220(11)
REM        01234567890123456789
REM ************* SCREEN *************

REM Raindrops keep falling for my head for piano
REM A A A B A G F A A C2 C C D C B A A B G F E4
REM 72 72 72 64 72 81 91 72 72 121 60 60 53 60 64 72 72 64 81 91 47
