REM Raindrops Keep Falling On My Head
REM (c) 2017 by Jason Gruetzmacher
REM
REM ------------------------------
REM The goal of the game is to collect enough raindrops to play the first 4 bars (+1 note!)
REM of the BJ Thomas classic song of the same name in the least amount of time you can
REM 
REM Press your joystick button to start a game
REM ------------------------------

REM set our graphics mode and establish memory addresses
REM - S=main screen
REM - R=reset screen
REM - T=animation screen
REM our two string arrays are to set character graphics (A) and for our music (U)
REM we will replace the x, y, and z characters with our head (xy) and our raindrop (z)
REM Q=256 just saves me space
000 DIM A$(24),U$(43)
010 A=ADR(A$):U=ADR(U$)
020 Q=256
030 A$="\01\03\7F\FD\7F\3C\0E\03\80\C0\FE\BF\FE\3C\70\C0\00\08\1C\3E\3E\3E\1C\00"
100 U$="\00\48\00\48\00\48\00\40\00\48\00\51\00\58\00\48\00\48\00\3C\00\3C\00\3C\00\35\00\3C\00\40\00\48\00\48\00\40\00\51\00\58\00\2F\2F"
REM this next set of code is mostly standard memory related code, 
REM which I won't explain.
REM The variables of note:
REM - S = address of screen
REM - V = address for row where collisions can occur
REM - P = address for row where player sits
REM - R = will contain a copy of the screen, so I can reset game
REM - T = used to animate raindrops/coffee cup
110 M=PEEK(106)
120 N=M-16
130 POKE 106,N
200 GR. 18
210 S=DPEEK(88)
220 V=S+200
230 P=V+20
240 R=(M-8)*Q
250 T=R+Q
260 O=PEEK(756)*Q
270 POKE 756,N
280 N=N*Q
300 MOVE O,N,Q*4
310 MOVE A,N+448,24

REM draw the screen, then save it at address R for resetting it
320 POS. 0,0:?#6;"drops 0"
330 POS. 9,11:?#6;"\58\59"
400 MOVE S,R,Q

REM infinite loop, so that you can start a new game just by pushing button
410 DO
REM   wait for user to press trigger to begin, then reset screen
420   SO.
430   WHILE STRIG(0)=1:WEND
440   MOVE R,S,Q
REM   L is the time we started.  Used to determine how long we've been playing
450   L=TIME
REM   Z=horizonal position of player minus 1.  
REM   H=number of drops collected
REM   C=keeps track of trips through repeat/until loop
460   Z=8:H=0:C=0
REM   ***** main game loop start *****
500   REPEAT
510     C=C+1
REM     look for player movement
520     IF C MOD 3 = 0
REM       K = +1/-1, representing a move right or left
REM       STICK(0):  11=left, 7=right
530       K=(9-STICK(0))/2
540       IF (K=1 AND Z<17) OR (K=-1 AND Z>0)
550         MOVE P+Z,T,4
600         Z=Z+K
610         MOVE T,P+Z,4
620       ENDIF
REM     check for raindrop collisions
630     ELSE:IF C MOD 3 = 1
640       FOR I=1 TO 2
700         IF PEEK(V+Z+I)<>0
720           POKE V+Z+I,0
730           H=H+1
740         ENDIF
750       NEXT I
800       SO. 0,PEEK(U+(H MOD 43)),10,5
REM     move existing and add new raindrops
810     ELSE
820       MOVE S,T,Q
830       MOVE T+20,S+40,180
840       POS. 2+RAND(15),2:?#6;"\DA"
850       POS. 6,0:?#6;H
860     ENDIF:ENDIF
940     POS. 17,0:?#6;INT((TIME-L)/60)
950   UNTIL H>42
REM   end
960   POS. 9,0:?#6;"\E5\EE\E4"
REM   ***** main game loop end *****
970 LOOP

REM   X=58, Y=59, Z=5A.  (orange)
REM     Lowercase = 78,79,7A    (green)
REM     Inverse = D8,D9,DA      (blue) 
REM     Inverse Lowercase = F8,F9,FA   (red)

REM ************* SCREEN *************
REM        01234567890123456789
REM   0( 0)DROPS 0          30  0 ( 0)
REM  20( 1)                    20 ( 1)
REM  40( 2)                    40 ( 2)
REM  60(03)                    60 ( 3)
REM  80(04)                    80 ( 4)
REM 100(05)                    100( 5)
REM 120(06)                    120( 6)
REM 140(07)                    140( 7)
REM 160(08)                    160( 8)
REM 180(09)                    180( 9)
REM 200(10)                    200(10)
REM 220(11)         yz         220(11)
REM        01234567890123456789
REM ************* SCREEN *************

REM Raindrops keep falling for my head for piano
REM A A A B A G F A A C2 C C D C B A A B G F E4
REM as decimals
REM 72 72 72 64 72 81 91 72 72 121 60 60 53 60 64 72 72 64 81 91 47
