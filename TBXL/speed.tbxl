70 REM ******** SPEED DEMON ********
80 REM Original game by JOHN MAGDZIARZ, 1982
90 REM Update by Jason Gruetzmacher, 2016

REM SHUT OFF SCREEN TEMPORARILY
100 POKE 559,0
REM OPEN KEYBOARD FOR DIRECT
110 OPEN #1,4,0,"K:"
120 A=PEEK(106):POKE 106,A-5
130 O=PEEK(756)*256
140 CP=PEEK(106)+1:POKE 756,CP
150 CHAR=CP*256
REM COPY CHAR SET FROM ROM TO RAM
160 MOVE O,CHAR,1024
REM POKE DATA FOR ALTERED CHARACTERS SET
170 DIM A$(104)
180 A$="\88\88\FF\FF\FF\FF\88\88\3C\3C\BE\BE\3C\3C\BE\BE\22\22\FF\FF\FF\FF\22\22\BE\BE\3C\3C\BE\BE\3C\3C\88\88\55\55\55\55\88\88\14\14\96\96\14\14\96\96\22\22\55\55\55\55\22\22\96\96\14\14\96\96\14\14\00\00\14\14\14\14\00\00\CC\CC\33\33\CC\CC\33\33\44\44\11\11\44\44\11\11\44\44\11\11\44\44\11\11\08\2A\A8\2A\AA\AA\A8\28"
200 MOVE ADR(A$),CHAR+8*69,104
220 GRAPHICS 0:POKE 756,CP:POKE 559,0
230 POKE 82,0

REM ALTER DISPLAY LIST
240 DL=DPEEK(560)
250 POKE DL+3,68
260 FOR I=6 TO 23:POKE DL+I,4:NEXT I
270 POKE DL+28,4

REM set screen colors
280 POKE 708,26 : POKE 709,0 : POKE 710,198 : POKE 711,72 : POKE 712,5

REM populate our three string arrays and other variables
290 DIM HOR$(39),VERT$(57),CR$(8)
300 FOR I=1 TO 39:HOR$(I,I)="\0D":NEXT I
310 FOR I=0 TO 18:J=3*I+1:VERT$(J,J+2)="\0D\1D\1E":NEXT I
320 CR$="\85\86\87\88\89\8A\8B\8C"
330 UP=14:DWN=13:LEFT=11:RIGHT=7:FINISHX=24:FINISHY=16

REM ***** reset game - aka (re)Start Game *****
500 # RESET_GAME
510 POKE 559,0:CLS:RESTORE 550
520 X1=23:Y1=16:S1L=RIGHT:LAP1=0:P1=1
530 X2=23:Y2=17:S2L=RIGHT:LAP2=0:P2=5

REM draw playfield
540 FOR I=1 TO 9:READ A,B,C:POSITION A,B:? HOR$(1,C):NEXT I
550 DATA 0,0,39,0,18,39,1,9,18,3,3,3,3,6,18,3,12,13,15,15,21,26,6,7,28,3,8
560 FOR I=1 TO 18:READ A,B,C:D=3*C-2:POSITION A,B:? VERT$(1,D):NEXT I
570 DATA 0,1,17,3,4,2,3,13,3,5,4,2,6,14,4,9,1,3,9,13,3,12,15,3,13,3,3
580 DATA 15,13,2,17,1,3,18,10,3,21,3,12,25,1,12,29,9,6,32,7,6,35,4,11,38,1,17
590 POSITION 24,15:? "\0F\1D\1E\0E\1D\1E\0E\1D\1E\10":REM These are the start/finish line flags

REM important memory pokes for Display List, hiding cursor, ensuring we can scan for the Start button later
600 POKE 559,34:POKE 752,1:POKE 53279,0

REM have user select level of difficulty: E=69, H=72
610 SOUND 0,170,4,4
620 POSITION X1,Y1:? CR$(1,1)
625 POSITION X2,Y2:? CR$(5,5)
630 POSITION 14,20:? "SELECT LEVEL"
640 POSITION 12,21:? "(E)asy or (H)ard";
650 LEV=0:WHILE LEV<>69 AND LEV<>72:GET #1,LEV:WEND
660 SC=35*(24-LEV/3)+10

REM Information Pane
700 POSITION 13,19:?"|":POSITION 26,19:?"|"
710 POSITION  1,20:?"\0E SPEED   \0E | TIME   LAP | \0E   SPEED \0E"
720 POSITION  1,21:?"\02   DEMON \16 |  0      0  | \02 DEMON   \16"
730 POSITION 13,22:?"|":POSITION 26,22:?"|"

REM countdown timer
800 FOR G=1 TO 5:POSITION 23,21:? 6-G:SOUND 0,40,10,4:PAUSE 10:SOUND 0,0,0,0:PAUSE 10:NEXT G
820 SOUND 0,85,6,5
830 START=INT(TIME/60)

REM ***** Main Game Kernal *****
1000 DO
1010   NOW=INT(TIME/60)-START
1015   POSITION 16,21:? NOW
1020   IF PEEK(53279)=6 THEN GO# RESET_GAME

REM -- player 1 --
1030   S=STICK(0)
1040   IF S<>UP AND S<>DWN AND S<>LEFT AND S<>RIGHT THEN S=S1L
1060   X=X1:Y=Y1:P1L=P1:LAP=LAP1:C=0
1070   GOSUB 2000
1080   X1=XN:Y1=YN:P1=PN:S1L=S:LAP1=LAP
1090   IF LAP1=4 THEN GO# GAME_OVER

REM -- player 2 --
1100   X=X2:Y=Y2:P2L=P2:LAP=LAP2:C=4
1110   GOSUB 2000
1120   X2=XN:Y2=YN:P2=PN:S2L=S:LAP2=LAP

1600 LOOP

REM set X/Y coordinates for where player will end up next
2000 IF S=RIGHT
2010   XN=X+1:YN=Y:P=1
2020 ELSE:IF S=UP
2030   XN=X:YN=Y-1:P=2
2040 ELSE:IF S=LEFT
2050   XN=X-1:YN=Y:P=3
2060 ELSE:IF S=DWN
2070   XN=X:YN=Y+1:P=4
2080 ELSE
2090   XN=X:YN=Y
2100 ENDIF:ENDIF:ENDIF:ENDIF

2110 LOCATE XN,YN,ZN:POSITION XN,YN:PUT #6,ZN
2120 IF ZN=17
2130   CRASH=1:CANMOVE=1
2140 ELSE:IF ZN=13
2150   CRASH=1:CANMOVE=0
2160 ELSE:IF ZN=14 AND S=LEFT
2170   CRASH=0:CANMOVE=0
2180 ELSE
2190   CRASH=0:CANMOVE=1
2200 ENDIF:ENDIF:ENDIF

2500 IF CANMOVE=1
2510   IF ZN=14:Z=14:ELSE:Z=32:ENDIF
2520   POSITION X,Y:PUT #6,Z
2530   POSITION XN,YN:? CR$(P+C,P+C)

REM play a sound when the player turns
2540   IF P<>PL
2550     SOUND 2,5,0,8:SOUND 3,2,10,8
2560     PAUSE 1
2570     SOUND 2,0,0,0:SOUND 3,0,0,0
2580   ENDIF

REM check to see if we're crossing the finish line
2590   IF Z=14
2600     LAP=LAP+1
2610     SOUND 2,50,12,10:PAUSE 1:SOUND 2,0,0,0
2620     POSITION 23,21:? LAP
2640   ENDIF

REM place an oil slick randomly.  SC is set based on the level the user selected, it's either 10 or 35
2650   OIL=INT(RND(0)*SC+1)
2660   LOCATE X,Y,Z:POSITION X,Y:PUT #6,Z
2670   IF OIL=7 AND Z=32
2680     POSITION X,Y:PUT #6,17
2690   ENDIF
2720 ENDIF

REM if the player crashed, play the crash sounds and animation
2730 IF CRASH=1
2740   FOR I=1 TO 8
2750     J=I MOD 4 + 1
2760     SOUND 1,100,0,2*(8-I)
2770     POSITION X,Y:? CR$(J+C,J+C)
2780     PAUSE 1
2790   NEXT I
2800   POSITION X,Y:? CR$(P1L+C,P1L+C)
2810   S=0
2820 ENDIF

2990 RETURN

REM game_over
9700 # GAME_OVER
9710 DSOUND
9720 POSITION 15,22:? "GAME  OVER"
9730 POKE 53279,0
9740 WHILE PEEK(53279)<>6:WEND:REM while player hasn't press start, loop
9750 GO# RESET_GAME