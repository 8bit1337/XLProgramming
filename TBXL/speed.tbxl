70 REM ******** SPEED DEMON ********
80 REM Original game by JOHN MAGDZIARZ, 1982
90 REM Update by Jason Gruetzmacher, 2016

REM SHUT OFF SCREEN TEMPORARILY
100 POKE 559,0
REM OPEN KEYBOARD FOR DIRECT
110 OPEN #1,4,0,"K:"
120 A=PEEK(106):POKE 106,A-5
130 O=PEEK(756)*256
140 CP=PEEK(106)+1:POKE 756,CP
150 CHAR=CP*256
REM COPY CHAR SET FROM ROM TO RAM
160 MOVE O,CHAR,1024
REM POKE DATA FOR ALTERED CHARACTERS SET
170 DIM A1$(32),A2$(40)
180 A1$="\88\88\FF\FF\FF\FF\88\88\3C\3C\BE\BE\3C\3C\BE\BE\22\22\BF\BF\BF\BF\22\22\BE\BE\3C\3C\BE\BE\3C\3C"
190 A2$="\00\00\14\14\14\14\00\00\CC\CC\33\33\CC\CC\33\33\FF\55\FF\AA\3C\14\3C\00\00\3C\14\3C\AA\FF\55\FF\08\2A\A8\2A\AA\AA\A8\28"
200 MOVE ADR(A1$),CHAR+8*69,32
210 MOVE ADR(A2$),CHAR+8*77,40
220 GRAPHICS 0:POKE 756,CP:POKE 559,0
230 POKE 82,0

REM ALTER DISPLAY LIST
240 DL=DPEEK(560)
250 POKE DL+3,68
260 FOR I=6 TO 23:POKE DL+I,4:NEXT I
270 POKE DL+28,4

REM set screen colors
280 POKE 708,26 : POKE 709,0 : POKE 710,198 : POKE 711,72 : POKE 712,5

REM populate our three string arrays and other variables
290 DIM HOR$(39),VERT$(57),CR1$(4)
300 FOR I=1 TO 39:HOR$(I,I)="\0D":NEXT I
310 FOR I=0 TO 18:J=3*I+1:VERT$(J,J+2)="\0D\1D\1E":NEXT I
320 CR1$="\85\86\87\88"
330 UP=14:DWN=13:LEFT=11:RIGHT=7:FLX=24:FLY=16

REM ***** reset game - aka (re)Start Game *****
500 # RESET_GAME
510 POKE 559,0:CLS:RESTORE 550
520 X1=23:Y1=16:SLAST=RIGHT:LAP=-1:LP=1

REM draw playfield
540 FOR LOUP=1 TO 9:READ I,J,K:POSITION I,J:? HOR$(1,K):NEXT LOUP
550 DATA 0,0,39,0,18,39,1,9,18,3,3,3,3,6,18,3,12,13,15,15,21,26,6,7,28,3,8
560 FOR LOUP=1 TO 18:READ I,J,K:L=3*K-2:POSITION I,J:? VERT$(1,L):NEXT LOUP
570 DATA 0,1,17,3,4,2,3,13,3,5,4,2,6,14,4,9,1,3,9,13,3,12,15,3,13,3,3
580 DATA 15,13,2,17,1,3,18,10,3,21,3,12,25,1,12,29,9,6,32,7,6,35,4,11,38,1,17
590 POSITION FLX,FLY-1:? "\0E\1D\1D\1D\1E\0E":REM These are the start/finish line flags

REM important memory pokes for Display List, hiding cursor, ensuring we can scan for the Start button later
600 POKE 559,34:POKE 752,1:POKE 53279,0

REM have user select level of difficulty: E=69, H=72
610 SOUND 0,170,4,4
620 POSITION X1,Y1:? CR1$(LP,LP)
630 POSITION 14,20:? "SELECT LEVEL"
640 POSITION 12,21:? "(E)asy or (H)ard";
650 LEV=0:WHILE LEV<>69 AND LEV<>72:GET #1,LEV:WEND
660 SC=35*(24-LEV/3)+10

REM Information Pane
700 POSITION 13,19:?"|":POSITION 26,19:?"|"
710 POSITION  1,20:?"\0E SPEED   \0E | TIME   LAP | \0E   SPEED \0E"
720 POSITION  1,21:?"\02   DEMON \16 |  0      0  | \02 DEMON   \16"
730 POSITION 13,22:?"|":POSITION 26,22:?"|"

REM countdown timer
800 FOR G=1 TO 5:POSITION 23,21:? 6-G:SOUND 0,40,10,4:FOR T=1 TO 100:NEXT T:SOUND 0,0,0,0:FOR T=1 TO 100:NEXT T:NEXT G
820 SOUND 0,85,6,5
830 START=INT(TIME/60)

REM ***** Main Game Kernal *****
1000 DO
1010   NOW=INT(TIME/60)-START
1015   POSITION 16,21:? NOW
1020   IF PEEK(53279)=6 THEN GO# RESET_GAME

REM read the joystick
1030   S=STICK(0)
1040   IF S<>UP AND S<>DWN AND S<>LEFT AND S<>RIGHT THEN S=SLAST
1040   SLAST=S:LP=P1

REM stop the player from cheating by going left over the start/finish line
1060   IF X1=FLX+1 AND Y1>=FLY AND S=LEFT THEN S=0

REM set X/Y coordinates for where player will end up next
1070   IF S=RIGHT
1080     X1L=X1+1:Y1L=Y1:P1=1
1090   ELSE:IF S=UP
1100     X1L=X1:Y1L=Y1-1:P1=2
1110   ELSE:IF S=LEFT
1120     X1L=X1-1:Y1L=Y1:P1=3
1130   ELSE:IF S=DWN
1140     X1L=X1:Y1L=Y1+1:P1=4
1150   ELSE
1160     X1L=X1:Y1L=Y1
1170   ENDIF:ENDIF:ENDIF:ENDIF

REM check for collisions:  Z1=13 is a barrier, Z1=17 is an oil slick
1180   LOCATE X1L,Y1L,Z1:POSITION X1L,Y1L:PUT #6,Z1
1190   IF Z1=17
1200     CRASH=1:CANMOVE=1
1210   ELSE:IF Z1=13
1220     CRASH=1:CANMOVE=0
1230   ELSE:IF Z1>=133 AND Z1<=136
1240     CRASH=0:CANMOVE=0
1250   ELSE
1260     CRASH=0:CANMOVE=1
1270   ENDIF:ENDIF:ENDIF

REM if the player can move, move them, potentially place an oil slick, and check lap counter
1280   IF CANMOVE=1
1290     POSITION X1,Y1:?" ":POSITION X1L,Y1L:? CR1$(P1,P1)
1300     IF P1<>LP
1310       REM if the car is turning, play the car turn sound
1320       SOUND 2,5,0,8:SOUND 3,2,10,8:PAUSE 1:SOUND 2,0,0,0:SOUND 3,0,0,0:ENDIF
1330     IF X1=FLX AND Y1>=FLY
1341       REM if we've passed the start/finish line, increment the lap counter
1350       LAP=LAP+1
1360       SOUND 2,50,12,10:PAUSE 1:SOUND 2,0,0,0
1370       POSITION 23,21:? LAP
1380       IF LAP=3 THEN GO# GAME_OVER:ENDIF
1391     REM place an oil slick randomly based on which level the user chose
1400     I=INT(RND(0)*SC+1)
1410     IF I=7
1420       LOCATE LX,LY,Z
1430       IF Z=32
1440         POSITION LX,LY:? CHR$(17):ENDIF:ENDIF
1450     X1=X1L:Y1=Y1L
1460   ENDIF

REM if the player crashed, play the crash sounds and animation
1470   IF CRASH=1
1480     FOR I=1 TO 8
1490       J=I MOD 4 + 1
1500       SOUND 1,100,0,2*(8-I)
1510       POSITION X1,Y1:? CR1$(J,J)
1520       PAUSE 1
1530     NEXT I
1540     POSITION X1,Y1:? CR1$(LP,LP)
1550   ENDIF
1560 LOOP

REM game_over
1600 # GAME_OVER
1610 DSOUND
1620 POSITION 15,22:? "GAME OVER"
1630 POKE 53279,0
1640 WHILE PEEK(53279)<>6:WEND:REM while player hasn't press start, loop
1650 GO# RESET_GAME