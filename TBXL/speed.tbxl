70 REM ******** SPEED DEMON ********
80 REM Original game by JOHN MAGDZIARZ, 1982
90 REM Update by Jason Gruetzmacher, 2016

REM SHUT OFF SCREEN TEMPORARILY
180 POKE 559,0
REM OPEN KEYBOARD FOR DIRECT
190 OPEN #1,4,0,"K:"
200 A=PEEK(106):POKE 106,A-5
205 O=PEEK(756)*256
210 CP=PEEK(106)+1:POKE 756,CP
220 CHAR=CP*256
REM COPY CHAR SET FROM ROM TO RAM
225 MOVE O,CHAR,1024
REM POKE DATA FOR ALTERED CHARACTERS SET
265 DIM A1$(32),A2$(40)
275 A1$="\88\88\FF\FF\FF\FF\88\88\3C\3C\BE\BE\3C\3C\BE\BE\22\22\BF\BF\BF\BF\22\22\BE\BE\3C\3C\BE\BE\3C\3C"
285 A2$="\00\00\14\14\14\14\00\00\CC\CC\33\33\CC\CC\33\33\FF\55\FF\AA\3C\14\3C\00\00\3C\14\3C\AA\FF\55\FF\08\2A\A8\2A\AA\AA\A8\28"
295 MOVE ADR(A1$),CHAR+8*69,32
305 MOVE ADR(A2$),CHAR+8*77,40
400 GRAPHICS 0:POKE 756,CP:POKE 559,0
405 POKE 82,0

REM ALTER DISPLAY LIST
410 DL=DPEEK(560)
420 POKE DL+3,68
430 FOR I=6 TO 23:POKE DL+I,4:NEXT I
440 POKE DL+28,4

REM SET SCREEN COLORS, REGISTERS 0-4
450 POKE 708,26 : POKE 709,0 : POKE 710,198 : POKE 711,72 : POKE 712,5

REM ***** (re)Start Game *****
480 DIM HOR$(39),VERT$(57),CR1$(4)
490 POKE 559,0:CLS:RESTORE 530:S1=7
500 FOR I=1 TO 39:HOR$(I,I)="\0D":NEXT I
510 FOR I=0 TO 18:J=3*I+1:VERT$(J,J+2)="\0D\1D\1E":NEXT I

REM DRAW PLAYFIELD
520 FOR LOUP=1 TO 9:READ I,J,K:POSITION I,J:? HOR$(1,K):NEXT LOUP
530 DATA 0,0,39,0,18,39,1,9,18,3,3,3,3,6,18,3,12,13,15,15,21,26,6,7,28,3,8
540 FOR LOUP=1 TO 18:READ I,J,K:L=3*K-2:POSITION I,J:? VERT$(1,L):NEXT LOUP
550 DATA 0,1,17,3,4,2,3,13,3,5,4,2,6,14,4,9,1,3,9,13,3,12,15,3,13,3,3
560 DATA 15,13,2,17,1,3,18,10,3,21,3,12,25,1,12,29,9,6,32,7,6,35,4,11,38,1,17

REM Information Pane
630 POSITION 3,19:? "| DRIVER  |TIME|LAP|"
640 POSITION 3,20:? "      "
650 POSITION 3,21:? "|PLAYER #1| 0  | 0 |"
660 POSITION 3,22:? "     "
670 POSITION 24,19:? "|";CHR$(29);CHR$(30);"|";CHR$(29);CHR$(30);"|";CHR$(29);CHR$(30);"|"
680 POSITION 26,20:? "\0E SPEED  \0E"
690 POSITION 26,21:? "\02  DEMON \16"
700 POSITION 24,15:? CHR$(14):POSITION 24,18:? CHR$(14)
710 FOR I=1 TO 4:CR1$(I,I)=CHR$(I+132):NEXT I:LAP1=-1:AG=0:LP=1
720 X1=23:Y1=16:POSITION X1,Y1:? CR1$(1,1)

REM the first address is for display lists, the second is to disable the cursor, the last address has to do with disabling basic
730 POKE 559,34:POKE 752,1:POKE 53279,0

REM determine the level
740 GOSUB 1740


REM START OF GAME ROUTINE
820 FOR G=5 TO 1 STEP -1:POSITION 23,21:? G:SOUND 0,40,10,4:FOR T=1 TO 100:NEXT T:SOUND 0,0,0,0:FOR T=1 TO 10:NEXT T:NEXT G
830 POSITION 23,21:? " "
840 SOUND 0,85,6,5
850 START=INT(TIME/60)
870 NOW=INT(TIME/60)-START:POSITION 15,21:? NOW:IF NOW>98 THEN 480
880 IF PEEK(53279)=6 THEN 490
900 IF AG=1 THEN 1200

REM DO
REM   S=STICK(0)
REM   IF (S=14 OR S=13) AND (X1=24) AND (Y1=16 OR Y=17) THEN S=7
REM   IF S=7
REM     S1=S:X1L=X1+1:Y1L=Y1:P1=1:GOTO 1070
REM   ELSE:IF S=11
REM     S1=S:X1L=X1-1:Y1L=Y1:P1=3:IF (X1=24 OR X1=25) AND (Y1=16 OR Y1=17) THEN 870
REM   ELSE:IF S=13
REM     S1=S:X1L=X1:Y1L=Y1+1:P1=4
REM   ELSE:IF S=14
REM     S1=S:X1L=X1:Y1L=Y1-1:P1=2
REM   ENDIF:ENDIF:ENDIF:ENDIF
REM LOOP

REM READ JOYSTICK.  S1 is the last valid joystick position
910 S=STICK(0):IF S=15 OR S=10 OR S=6 OR S=5 OR S=9 THEN S=S1
920 S1=S
940 ON S-4 GOTO 870,870,980,0,870,870,1010,0,1030,1040
950 GOTO 910

980 X1L=X1+1:Y1L=Y1:P1=1:GOTO 1070
1010 X1L=X1-1:Y1L=Y1:P1=3:IF (X1=24 OR X1=25) AND (Y1=16 OR Y1=17) THEN 870
1020 GOTO 1070
1030 X1L=X1:Y1L=Y1+1:P1=4:GOTO 1050
1040 X1L=X1:Y1L=Y1-1:P1=2
1050 IF (S=14 OR S=13) AND (X1=24) AND (Y1=16 OR Y=17) THEN S=7:GOTO 940

REM CHECK COLLISIONS
1070 LOCATE X1L,Y1L,Z1:POSITION X1L,Y1L:PUT #6,Z1:LX=X1:LY=Y1
1080 IF Z1=17 THEN POSITION X1,Y1:? " ":X1=X1L:Y1=Y1L
1090 IF Z1<>32 THEN 1170

REM MOVE CAR
1110 POSITION X1,Y1:? " ":POSITION X1L,Y1L:? CR1$(P1,P1):IF P1<>LP THEN GOSUB 1350
1120 LO=P1:X1=X1L:Y1=Y1L
1130 IF X1=24 AND (Y1=16 OR Y1=17) THEN LAP1=LAP1+1:SOUND 2,50,12,10:FOR T=1 TO 10:NEXT T:POSITION 20,21:? LAP1
1140 SOUND 2,0,0,0
1150 IF LAP1=3 THEN POSITION X1,Y1:? " ":SOUND 0,0,0,0:GOSUB 1290:GOTO 640
1160 GOSUB 1230:GOTO 870

1170 K=INT(RND(0)*3+1)
1180 FOR I=14 TO 0 STEP -2:SOUND 1,100,0,I:FOR T=1 TO 10:NEXT T:NEXT I
1190 FOR G=1 TO K:FOR I=1 TO 4:POSITION X1,Y1:? CR1$(I,I):AG=1:GOTO 870
1200   NEXT I:NEXT G:AG=0:POSITION X1,Y1:? CR1$(P1,P1)
1210 GOTO 910

REM OIL SLICK PLACEMENT ROUTINE
1230 I=INT(RND(0)*SC+1)
1240 IF I=7 THEN GOSUB 1260
1250 RETURN 

1260 LOCATE LX,LY,Z:IF Z=32 THEN POSITION LX,LY:? CHR$(17)
1261 RETURN 

1270 POSITION LX,LY:PUT #6,Z
1271 RETURN 

1290 POSITION 26,20:? "GAME OVER!":POSITION 25,21:? "             "
1300 POKE 53279,0
1310 S2=PEEK(53279):IF S2<>6 THEN 1310
1320 GOTO 490
1330 RETURN 

REM CAR TURNING SOUND
1350 SOUND 2,5,0,8:SOUND 3,2,10,8:FOR X=1 TO 10:NEXT X:SOUND 2,0,0,0:SOUND 3,0,0,0
1360 RETURN

1740 SOUND 0,170,4,4
1750 POSITION 26,19:? "WHAT LEVEL?";:GET #1,LEV
1760 IF LEV=49 OR LEV=50
1770   SC=35*(LEV-49)+10
1780 ELSE
1785   GOTO 1750:ENDIF
1790 POSITION 26,19:? "           "
1800 RETURN