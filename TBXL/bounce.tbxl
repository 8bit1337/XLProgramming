Miyamoto14


10 REM BOUNCE BY JEFF JESSEE
20 REM (C)ADVENTURE INTERNATIONAL
100 GOSUB 2000

140 DFL=0:BOU=0:TAR=0:S=0
190 A$=STR$(PHS):FOR X=1 TO LEN(A$):A$(X,X)=CHR$(ASC(A$(X,X))+128):NEXT X
200 CLS
205 PRINT "\A0\A0\A0\A0\A0\A0\D0\D2\C5\D6\C9\CF\D5\D3\A0\C8\C9\C7\C8\A0\D3\C3\CF\D2\C5\A0=\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0"
210 FOR X=1 TO 21:PRINT "\A0                                   \A0":NEXT X
215 PRINT "\A0\D4\C1\D2\C7\C5\D4\D3\A0\A0\C4\C5\C6\CC\C5\C3\D4\CF\D2\D3\A0\A0\C2\CF\D5\CE\C3\C5\D3\A0\A0\D3\C3\CF\D2\C5\A0"
216 PRINT "\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0\A0";
218 POSITION 30,0:PRINT A$
220 YY=INT(RND(1)*9+7):FOR I=1 TO NTARGETS
230 X=INT(RND(1)*33+4):Y=INT(RND(1)*19+2):IF PEEK(SCREEN+X+Y*40)=96 OR Y=YY THEN 230
240 POKE SCREEN+X+Y*40,96:NEXT I
300 X=INT(RND(1)*30+2):Y=YY:Z=-1:DX=1:DY=0
400 SOUND 0,0,0,0:TX=X:TY=Y:X=X+DX:Y=Y+DY
410 GOSUB GCHAR:S=S+0.07
420 CHAR=PEEK(SCREEN+X+Y*40)
430 IF CHAR=70 THEN SOUND 0,130,10,10:DX=DX+Z:DY=DY+Z:Z=-Z:BOU=BOU+1:POSITION 26,23:PRINT BOU;:GOTO 600
500 IF CHAR>127 THEN Z=-Z:DX=-DX:DY=-DY:SOUND 0,150,10,10:BOU=BOU+1:POSITION 26,23:PRINT BOU;
600 IF LCHAR=84 THEN POSITION TX,TY:PRINT " ";
610 LCHAR=CHAR:IF CHAR=0 THEN POSITION X,Y:PRINT "\14";:LCHAR=84
620 IF CHAR<>96 THEN 400
630 SOUND 0,50,10,10:POSITION X,Y:SETCOLOR 2,3,6:PRINT "\14";
640 SOUND 0,100,12,12:PRINT "\1E ";
645 TAR=TAR+1:SCORE=INT((5000*TAR/NTARGETS+500*TAR-10*DFL-BOU)*TAR/(NTARGETS*S))
647 POSITION 34,23:PRINT "    \1E\1E\1E\1E";SCORE;
650 POSITION 6,23:PRINT TAR;:SETCOLOR 2,CX,2:IF NTARGETS<>TAR THEN 400
660 SOUND 0,0,0,0:IF SCORE>PHS THEN PHS=SCORE
670 SOUND 0,121,10,8:SOUND 1,96,10,8:SOUND 2,81,10,8:SOUND 3,60,10,8
672 CX=RND(1)*9+4
675 SETCOLOR 2,3,2:FOR XX=1 TO 400:NEXT XX:SETCOLOR 2,CX,2:SETCOLOR 4,CX,2
680 POSITION 14,2:PRINT "\A0\C7\C1\CD\C5\A0\CF\D6\C5\D2\A0";:POSITION 5,3:PRINT "\45\4E\54\45\52\20\4E\41\4D\45\20\54\4F\20\43\4F\4E\56\45\52\54\3A\20\3F\A0\C8\C9\D4\A0\C1\CE\D9\A0\CB\C5\D9\A0\C6\CF\D2\A0\C1\CE\CF\D4\C8\C5\D2\A0\C7";
685 FOR X=0 TO 3:SOUND X,0,0,0:NEXT X
690 POKE 764,255:OPEN #2,4,0,"K:":GET #2,X:CLOSE #2
700 GOTO 140
999 END 

1000 I=PEEK(764):IF I=255 THEN RETURN 
1010 IF I<>38 THEN RETURN 
1020 XX=PEEK(SCREEN+X+Y*40):IF XX=96 OR XX>127 OR XX=70 THEN 1050
1030 POSITION X,Y:PRINT "\06";:POSITION 16,23:DFL=DFL+1:PRINT DFL;
1050 POKE 764,255:RETURN 

2000 SETCOLOR 2,4,2
2010 SETCOLOR 2,4,2
2015 CLS
2020 PRINT "              BOUNCE\1D\1D"
2022 PRINT "In this game you try to bounce your"
2024 PRINT "ball into the targets. You do this"
2026 PRINT "by hitting the '/' key. This causes"
2028 PRINT "a '/' symbol to be placed immediately"
2030 PRINT "in front of the ball, and the ball"
2032 PRINT "bounces in the appropriate direction.\1D\1D"
2034 PRINT "You will best get the idea by"
2036 PRINT "playing a game. Start with the easy"
2038 PRINT "game first. Your score is based on"
2040 PRINT "the number of targets, deflectors,"
2042 PRINT "bounces, and time.\1D\1D"
2043 PRINT "Beware the corners!\1D\1D"
2044 PRINT "1 - EASY (5 TARGETS)"
2046 PRINT "2 - HARDER (10 TARGETS)"
2048 PRINT "3 - HARDEST (20 TARGETS)\1D"
2049 PRINT "Difficulty?";
2050 POKE 764,255:OPEN #2,4,0,"K:":GET #2,X:CLOSE #2
2052 NTARGETS=5
2054 IF X=50 THEN NTARGETS=10
2056 IF X=51 THEN NTARGETS=20
2100 DIM A$(40)
2110 GRAPHICS 0:GCHAR=1000:CX=9
2120 SCREEN=PEEK(88)+PEEK(89)*256:POKE 752,1
2130 SETCOLOR 2,9,2:SETCOLOR 4,9,2
2140 RETURN 