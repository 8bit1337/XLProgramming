REM CRAZY BALLOON II

REM Q is a constant, because we use 256 a lot and it saves characters
REM B$ contains the graphics for the balloon.  Note there are preceding and trailing zeros to make animation easier
REM B is the address where B$ resides
REM Z$ and Z are used to erase memory quickly
REM N is the new top of memory
REM P is the address of the , where the balloon starts
REM X is the horizontal position of the balloon
REM Y is the vertical position of the balloon

REM SETUP OUR BALLOON GRAPHICS
000 BHEIGHT=16
001 DIM B$(BHEIGHT):B=ADR(B$):B$="\00\1C\36\2E\2E\3E\3E\1C\08\08\08\08\06\00\00\00"

REM SETUP THE ASTERISKS
005 DIM A$(20):A=ADR(A$):A$="\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A"

REM SETUP MUSIC
REM 006 DIM U$(26):U=ADR(U$):U$="\51\48\40\35\35\2D\35\40\51\48\40\40\48\51\48\51\3C\3C\2D\2D\2D\35\35\40\51\48"

REM BORING MEMORY RELATED STUFF
010 Q=256:DIM Z$(Q):Z=ADR(Z$)

REM INITIALIZE
100 N=PEEK(106)-8:POKE 106,N:P=(N+4)*Q
110 GOS. 999
120 GR. 1
130 S=DPEEK(88): REM The beginning of screen memory, for making the asterisk maze
140 POKE 752,1:  REM Turn off the cursor
150 POKE 710,0:  REM Set BG colour for our text area
160 POKE 559,62: REM Set DMACTL: Single Line & Standard Playfield
170 POKE 53277,3:REM Set GRACTL: Players & Missles
180 POKE 54279,N:REM Tell ANTIC PMBASE is as N
200 POKE 53256,1:REM Set Player 0 (Balloon) graphics to double width
210 POKE 704,72: REM Set Player 0 colour
220 POKE 708,172:REM Set colour of Asterisks
230 MOVE Z,P,Q:  REM Clear out P/M area for Player 0

REM START
300 MOVE A,S+380,20:REM Create a row of asterisks as the bottom of the play area
310 DO
320   GOS. 999
340   WHILE STRIG(0)=1:WEND:REM wait for user to press trigger to begin, then reset screen
350   REPEAT
360     C=C+1
370     K=(9-STICK(0))/2:REM K = +1/-1, representing a move right or left
400     IF C MOD 3 = 0 AND ((K=1 AND X<189) OR (K=-1 AND X>50)):REM Move balloon left or right
410       X=X+K
420       POKE 53248,X
430     ELSE:IF C MOD 3 = 1 AND STRIG(0)=0:REM Move balloon up when user presses button
440       Y=Y-3
450       MOVE B,Y,BHEIGHT
800     ELSE
810       Y=Y+1
820       MOVE B,Y,BHEIGHT:ENDIF:ENDIF:REM Move balloon down (gravity)
830       MOVE S+20,S,380:REM Move Asterisks up
840       MOVE Z,S+365,6

800     POKE 656,2:POKE 657,7:?INT(C/10):REM Print score
810   UNTIL PEEK(53252)>0:REM Stop when player hits something

890 LOOP:END
999 SO.:X=120:Y=P+X:MOVE B,Y,BHEIGHT:POKE 53248,X:C=0:POKE 656,2:POKE 657,0:PRINT "SCORE: 0     ":RETURN

REM OH SUSANNAH
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Gq Ah.
REM Ge Ge Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Gq Ah.
REM Ge Ge Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.
REM Gq Aq Bq Ch Ch  Fq Fh Fq Dq  Dq Bq Gq Ah.
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.

REM U$
REM Ge Ae Bq Dq Dq.Fe Dq Bq Gq.Ae Bq Bq Aq Gq Ah.Gq Ch Ch Fq Fh Fq Dq Dq Bq Gq Ah.
REM \51\48\40\35\35\2D\35\40\51\48\40\40\48\51\48\51\3C\3C\2D\2D\2D\35\35\40\51\48


REM G=81  \51
REM A=72  \48
REM B=64  \40
REM C=60  \3C
REM D=53  \35
REM E=47
REM E#=45 \2D (aka F)



