REM CRAZY BALLOON II
REM (c) 2018 by Jason "8BIT 1337" Gruetzmacher

REM VARIABLES OF NOTE
REM A$ contains the asterisks that make up the obstacles and it
REM B$ contains the graphics for the balloon, with extra padding to make animation easier
REM X is the horizontal position of the balloon
REM Y is the vertical position of the balloon
REM G is the space in memory where the gap starts
REM W is the width of the gap
REM C counts how many cycles through the code we've gone, and is used to control program flow and calculate the score

REM balloon graphics
000 DIM B$(17):B=ADR(B$):B$="\00\00\1C\36\2E\2E\3E\3E\1C\08\08\08\08\06\00\00\00"

REM asterisk maze
001 DIM A$(20):A=ADR(A$):A$="\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A"

REM music
002 DIM U$(17):U=ADR(U$):U$="\51\48\40\35\35\2D\35\40\51\48\40\40\48\51\48\00\00"

REM initialize
010 Q=256:DIM Z$(Q):Z=ADR(Z$)
100 N=PEEK(106)-8:POKE 106,N:P=(N+4)*Q
REM 110 GOS. 999
200 GR. 1:S=DPEEK(88):POKE 752,1:POKE 710,0:POKE 559,62:POKE 53277,3:POKE 54279,N:POKE 53256,1:POKE 704,72:POKE 708,172

REM Create a row of asterisks at the bottom of the play area
300 MOVE A,S+380,20
310 DO
320   GOS. 999
340   WHILE STRIG(0)=1:WEND
350   REPEAT
360     C=C+1
370     IF C MOD 2 = 1
REM       K = +1/-1, representing a move right or left
380       K=(9-STICK(0))/2
390       IF K=1 OR K=-1
400         X=X+K:POKE 53248,X:ENDIF
REM       elevate balloon
420       IF STRIG(0)=0
440         Y=Y-3:MOVE B,Y,17:ENDIF
460     ENDIF
600     IF C MOD BGSPEED = 0
601       V=V+1
602       SO. 0,PEEK(U+V MOD 17),10,5 
REM       balloon moves down automatically
610       Y=Y+2
620       MOVE B,Y,17
630       IF C MOD 10*BGSPEED=0
640         IF HOLEWIDTH>=4:HOLEWIDTH=HOLEWIDTH-1:ELSE:BGSPEED=ABS(BGSPEED-1):ENDIF
645       ELSE  
650         HOLESTART=HOLESTART+(RAND(3)-1)
655         IF HOLESTART<361:HOLESTART=361:ENDIF:IF HOLESTART>379-HOLEWIDTH:HOLESTART=379-HOLEWIDTH:ENDIF
660       ENDIF

REM       move background up and insert new gap
670       MOVE S+20,S,380:MOVE Z,S+HOLESTART,HOLEWIDTH
680     ENDIF

REM     update score
700     POKE 656,2:POKE 657,7:?INT(C/14)
REM   stop when the player hits the background
710   UNTIL PEEK(53252)>0
REM reset background and collision registers
800 PAUSE 20:MOVE Z,S,190:MOVE Z,S+190,190:POKE 53278,0
890 LOOP
999 SO.:MOVE Z,P,Q:X=120:Y=P+X:MOVE B,Y,17:POKE 53248,X:C=0:V=-1:HOLESTART=367:HOLEWIDTH=8:BGSPEED=7:POKE 656,2:POKE 657,0:?"SCORE:       ":RETURN



REM OH SUSANNAH
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Gq Ah.
REM Ge Ge Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Gq Ah.
REM Ge Ge Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.
REM Gq Aq Bq Ch Ch  Fq Fh Fq Dq  Dq Bq Gq Ah.
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.

REM PEEK/POKE ADDRESSES EXPLAINED
REM DPEEK(88)    = The beginning of screen memory, for making the asterisk maze
REM POKE 752,1   = Turn on(0) or off (1) the cursor
REM POKE 710,0   = Set BG colour for our text area
REM POKE 559,62  = Set DMACTL: Single Line & Standard Playfield
REM POKE 53277,3 = Set GRACTL: Players & Missles
REM POKE 54279,N = Telling ANTIC that PMBASE is at address N
REM POKE 53256,1 = Set Player 0 (Balloon) graphics to double width
REM POKE 704,72  = Set Player 0 colour to reddish
REM POKE 708,172 = Set colour of the asterisks to tealish
REM MOVE Z,P,Q   = Clear out P/M area for Player 0

