REM CRAZY BALLOON II

REM Q is a constant, because we use 256 a lot and it saves characters
REM B$ contains the graphics for the balloon.  Note there are preceding and trailing zeros to make animation easier
REM B is the address where B$ resides
REM Z$ and Z are used to erase memory quickly
REM N is the new top of memory
REM P is the address of the , where the balloon starts
REM X is the horizontal position of the balloon
REM Y is the vertical position of the balloon

REM SETUP OUR BALLOON GRAPHICS
000 GAP=16
001 DIM B$(GAP):B=ADR(B$):B$="\00\1C\36\2E\2E\3E\3E\1C\08\08\08\08\06\00\00\00"

REM SETUP THE ASTERISKS
005 DIM A$(20):A=ADR(A$):A$="\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A\0A"

REM SETUP MUSIC
REM 006 DIM U$(26):U=ADR(U$):U$="\51\48\40\35\35\2D\35\40\51\48\40\40\48\51\48\51\3C\3C\2D\2D\2D\35\35\40\51\48"

REM BORING MEMORY RELATED STUFF
010 Q=256:DIM Z$(Q):Z=ADR(Z$)
020 N=PEEK(106)-8:POKE 106,N:P=(N+4)*Q

REM INITIALIZE
030 GOS. 999
080 GR. 1
090 POKE 752,1:  REM TURN OFF CURSOR
095 POKE 710,0:  REM SET BG COLOUR FOR TEXT AREA
100 POKE 559,62: REM SET DMACTL: SINGLE LINE & STANDARD PLAYFIELD
110 POKE 53277,3:REM SET GRACTL: PLAYERS & MISSILES
120 POKE 54279,N:REM TELL ANTIC PMBASE IS AT N
130 POKE 53256,1:REM PLAYER #0 DOUBLE WIDTH
150 POKE 704,72: REM PLAYER #0 COLOR
155 POKE 708,172:REM COLOUR FOR THE ASTERISKS
160 MOVE Z,P,Q:  REM CLEAR OUT P/M AREA FOR PLAYER #0

REM GAME KERNAL START!
REM 200 DO
210   GOS. 999
226   POS.0,19:?#6;A$
REM 230   WHILE STRIG(0)=1:WEND:REM wait for user to press trigger to begin, then reset screen
240   REPEAT
250     C=C+1

300     K=(9-STICK(0))/2:REM K = +1/-1, representing a move right or left
310     IF C MOD 3 = 0 AND ((K=1 AND X<189) OR (K=-1 AND X>50))
330       X=X+K
340       POKE 53248,X
500     ELSE:IF C MOD 2 = 1 AND STRIG(0)=0
710       Y=Y-3
720       MOVE B,Y,GAP
800     ELSE
810       Y=Y+1
820       MOVE B,Y,GAP:ENDIF:ENDIF

970     POKE 656,2:POKE 657,7:LOCATE 9,(Y-P)/10+1.5,Z:PRINT (Y-P)/10,Z
975     PAUSE 5
980   UNTIL L=0 OR Y-P>=170

REM 990 LOOP
REM RESET GAME VARIABLES
998 END
999 SO.:X=120:Y=P+X:MOVE B,Y,GAP:POKE 53248,X:C=0:L=1:POKE 656,2:POKE 657,0:PRINT "SCORE:       ":RETURN

REM OH SUSANNAH
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Gq Ah.
REM Ge Ge Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Gq Ah.
REM Ge Ge Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.
REM Gq Aq Bq Ch Ch  Fq Fh Fq Dq  Dq Bq Gq Ah.
REM Ge Ae Bq Dq Dq. Fe Dq Bq Gq. Ae Bq Bq Aq Aq Gh.

REM U$
REM Ge Ae Bq Dq Dq.Fe Dq Bq Gq.Ae Bq Bq Aq Gq Ah.Gq Ch Ch Fq Fh Fq Dq Dq Bq Gq Ah.
REM \51\48\40\35\35\2D\35\40\51\48\40\40\48\51\48\51\3C\3C\2D\2D\2D\35\35\40\51\48


REM G=81  \51
REM A=72  \48
REM B=64  \40
REM C=60  \3C
REM D=53  \35
REM E=47
REM E#=45 \2D (aka F)



