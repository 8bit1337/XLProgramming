REM FIRST P/M EXAMPLE - MOVING A SHIP VERTICALLY UPWARDS FROM BASIC
5 DIM T$(14):T=ADR(T$):T$="\00\00\99\99\BD\BD\FF\FF\BD\BD\99\99\00\00"
6 DIM B$(256):B=ADR(B$)
10 POKE 106,PEEK(106)-8
20 PM=PEEK(106):PMBASE=PM*256
30 GR. 17
40 POKE 559,62:REM SET DMACTL - SINGLE LINE & STANDARD PLAYFIELD
50 POKE 53277,3:REM SET GRACTL - PLAYERS & MISSILES
60 POKE 54279,PM:REM TELL ANTIC PMBASE
70 POKE 53256,1:REM PLAYER #0 DOUBLE WIDTH
80 POKE 53248,100:REM PLAYER #0 HORIZONTAL POSITION
90 POKE 704,88:REM PLAYER #0 COLOR PINK
REM CLEAR OUT P/M AREA FOR PLAYER #0 ONLY
100 MOVE B,PMBASE+1024,256
120 REM READ PLAYER #0 DATA INTO PLAYER AREA BUT STARTING 100 BYTES INTO PAGE
130 SPOT=PMBASE+1024+100
140 MOVE T,SPOT,14
REM DELAY LOOP
160 PAUSE 20
REM MOVE PLAYER DATA UP TWO SCAN LINES AT A TIME - TEN TIMES
180 FOR J=1 TO 10
200   MOVE T,SPOT-2,14
220   SPOT=SPOT- 2:REM CURRENT PLAYER MEMORY LOCATION
230   NEXT J
235 REM MOVE PLAYER DATA DOWN TWO SCAN LINES AT A TIME - TWENTY TIMES
240 FOR J=1 TO 20
260   MOVE T,SPOT+2,14
280   SPOT=SPOT+2
290   NEXT J
REM MOVE PLAYER HORIZONTALLY RIGHT
310 FOR I=100 TO 180
320   POKE 53248,I:NEXT I
330 GOTO 330
REM PLAYER #0 DATA - SHIP
1005 DATA 0,0,153,153,189,189,255,255,189,189,153,153,0,0
REM 