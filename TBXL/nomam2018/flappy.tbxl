REM FIRST P/M EXAMPLE - MOVING A SHIP VERTICALLY UPWARDS FROM BASIC
000 Q=256:DIM T$(14):T=ADR(T$):T$="\00\00\99\99\BD\BD\FF\FF\BD\BD\99\99\00\00"
010 DIM Z$(Q):Z=ADR(Z$)
020 N=PEEK(106)-8:POKE 106,N:P=N*Q+1024
030 GR. 17

100 POKE 559,62:REM SET DMACTL - SINGLE LINE & STANDARD PLAYFIELD
110 POKE 53277,3:REM SET GRACTL - PLAYERS & MISSILES
120 POKE 54279,N:REM TELL ANTIC PMBASE IS AT N
REM 130 POKE 53256,1:REM PLAYER #0 DOUBLE WIDTH
140 POKE 53248,100:REM PLAYER #0 HORIZONTAL POSITION
150 POKE 704,88:REM PLAYER #0 COLOR PINK
160 MOVE Z,P,Q:REM CLEAR OUT P/M AREA FOR PLAYER #0 ONLY
170 XY=P+100:REM READ PLAYER #0 DATA INTO PLAYER AREA BUT STARTING 100 BYTES INTO PAGE

200 DO
210   SO.
220   MOVE T,XY,14
230   WHILE STRIG(0)=1:WEND:REM wait for user to press trigger to begin, then reset screen

REM STRIG(0)=0 means button pushed
REM MOVE PLAYER DATA UP TWO SCAN LINES AT A TIME - TEN TIMES
300 FOR J=1 TO 20
400   MOVE T,XY-2,14
420   XY=XY- 2:REM CURRENT PLAYER MEMORY LOCATION
430   NEXT J
435 REM MOVE PLAYER DATA DOWN TWO SCAN LINES AT A TIME - TWENTY TIMES
440 FOR J=1 TO 20
460   MOVE T,XY+2,14
480   XY=XY+2
490   NEXT J
REM MOVE PLAYER HORIZONTALLY RIGHT
510 FOR I=100 TO 180
520   POKE 53248,I:NEXT I

610 FOR I=180 TO 100 STEP -1
620   POKE 53248,I:NEXT I

999 LOOP