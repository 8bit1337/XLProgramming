REM SHMUP
REM (c) 2018 by Jason Gruetzmacher

REM ship graphics
000 DIM B$(8):B=ADR(B$):B$="\00\20\20\70\70\24\F8\00"

REM initialize
100 N=PEEK(106)-8:POKE 106,N:P=(N+4)*256
200 GR. 3
201 S=DPEEK(88):POKE 752,1:POKE 710,0:POKE 559,62:POKE 53277,3:POKE 54279,N:POKE 53256,1:POKE 704,72:POKE 708,172

REM Print explanatory text
300 POKE 656,3:POKE 657,0:?"--left & right steer, button elevates--"

301 FOR I=0 TO 255:POKE P+I,0:NEXT I
302 X=120:Y=P+180
303 FOR I=0TO 7:POKE Y+I,PEEK(B+I):NEXT I


310 DO
REM   wait for the player to start by pressing the trigger
340   WHILE STRIG(0)=1:WEND
REM   this resets everything, plus the background and collision register
345   GOS. 999:MOVE Z,S,190:MOVE Z,S+190,190:POKE 53278,0
350   REPEAT
360     C=C+1
370     IF C MOD 2=1
REM       K = +1/-1, representing a move right or left
380       K=(9-STICK(0))/2
390       IF ABS(K)=1
400         X=X+K:POKE 53248,X:ENDIF
REM       elevate balloon
420       IF STRIG(0)=0
440         Y=Y-3:IF Y<P+30:Y=P+30:ENDIF:MOVE B,Y,17:ENDIF
460     ENDIF
500     IF C MOD 7=0
REM       play music
510       V=V+1
520       SO. 0,PEEK(U+V MOD 62),10,5
530     ENDIF
600     IF C MOD D=0
REM       balloon moves down: because gravity
610       Y=Y+2
620       MOVE B,Y,17
REM       increase difficulty
630       IF C MOD 40*D=0
635         E=(E+1) MOD 2
640         IF E=0:IF W>4:W=W-1:ENDIF:ELSE:IF D>4:D=D-1:ENDIF:ENDIF
645       ELSE  
650         H=H+(RAND(3)-1)
REM         make sure the asterisks stay inside the screen
655         IF H<361:H=361:ENDIF:IF H>379-W:H=379-W:ENDIF
660       ENDIF

REM       move background up and insert new gap
670       MOVE S+20,S,380:MOVE Z,S+H,W
680     ENDIF
690     
REM     update score
700     POKE 656,0:POKE 657,22:?INT(C/14)
REM   stop when the player hits the background
710   UNTIL PEEK(53252)>0
890 SO.:PAUSE 70:LOOP
999 :POKE 53248,X:C=0:V=-1:H=367:W=7:D=7:E=0:POKE 656,0:POKE 657,15:?"SCORE: 0  ":RETURN


REM OH SUSANNAH
REM Ge Ae Bq Dq Dq. Ee Dq Bq Gq. Ae Bq Bq Aq Gq Ah.
REM Ge Ae Bq Dq Dq. Ee Dq Bq Gq. Ae Bq Bq Aq Aq Gh.
REM Ge Ae Bq Dq Dq. Ee Dq Bq Gq. Ae Bq Bq Aq Gq Ah.
REM Ge Ae Bq Dq Dq. Ee Dq Bq Gq. Ae Bq Bq Aq Aq Gh.
REM Ch Ch Eq Eh Eq Dq  Dq Bq Gq Ah.
REM Ge Ae Bq Dq Dq. Ee Dq Bq Gq. Ae Bq Bq Aq Aq Gh.

REM I ignored the exact timing of each note because of space limitations
REM the first letter is the note
REM the second letter is the length (e=eighth, q=quarter, h=half, . extends the note by half it's length)

REM ************* SCREEN *************
REM        0123456789012345678901234567890123456789
REM   0( 0) score #####                            0  ( 0)
REM  40( 1) |                                    | 40 ( 1)
REM  80( 2) |                                    | 80 ( 2)
REM 120( 3) |                                    | 120( 3)
REM 160( 4) |                                    | 160( 4)
REM 200( 5) |                                    | 200( 5)
REM 240( 6) |                                    | 240( 4)
REM 280( 7) |                                    | 280( 4)
REM 320( 8) |                                    | 320( 4)
REM 360( 9) |                                    | 360( 4)
REM 400(10) |                                    | 400( 4)
REM 440(11) |                                    | 440( 4)
REM 480(12) |                                    | 480( 4)
REM 520(13) |                                    | 520( 4)
REM 560(14) |                                    | 560( 4)
REM 600(15) |                                    | 600( 4)
REM 640(16) |                                    | 640( 4)
REM 680(17) |                                    | 680( 4)
REM 720(18) |                                    | 720( 4)
REM 760(19) |                                    | 760( 4)
REM 800(20) |                                    | 800( 4)
REM 840(21) |                                    | 840( 4)
REM 880(22) |                /-\                 | 880( 4)
REM 920(23) |                | |                 | 920( 4)
REM        0123456789012345678901234567890123456789

REM ************* SCREEN *************
