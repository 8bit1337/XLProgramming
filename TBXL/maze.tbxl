10 REM MAZE (C) 1990 SAWFISH SOFTWARE
20 REM PROG. BY MICHAEL GUNTERSDORFER

REM  DIMS
50 DIM F$(1002),A$(1),T$(6),PP(9)

REM  SCREEN 
80 GRAPHICS 0:POKE 559,0:POKE 752,1:POKE 710,0:POKE 708,134:POKE 709,12:? 
90 FOR I=15 TO 18:POKE DPEEK(88)-I,6:NEXT I
100 POSITION 9,9:? "M":POSITION 29,9:? "A":POSITION 9,10:? "Z":POSITION 29,10:? "E"
110 POSITION 31,18:? "(c) 1990":POSITION 31,19:? "sawfish":POSITION 31,20:? "software"
120 POSITION 14,12:? "please wait":POKE 559,34

REM  PREPARES 
150 FOR I=1 TO 1002:F$(I,I)="x":NEXT I
160 X=21:Y=12:AX=X:AY=Y:P=X+1+Y*40:F$(P,P)=" "
170 FOR Z=1 TO 150
180   IF RAND(2)=0:RX=RAND(2)*2-1:RY=0:ELSE :RX=0:RY=RAND(2)*2-1:ENDIF 
190   RX=RX*2:RY=RY*2
200   IF X+RX>39 OR X+RX<2 OR Y+RY>22 OR Y+RY<2 THEN GOTO 180
210   P=X+RX+1+(Y+RY)*40:A$=F$(P,P):IF A$=" " AND RAND(10)>0 THEN GOTO 180
220   P=X+1+Y*40:F$(P,P)=" ":P=X+RX/2+1+(Y+RY/2)*40:F$(P,P)=" ":X=X+RX:Y=Y+RY:P=X+1+Y*40:F$(P,P)=" ":NEXT Z

230 P=AX+1+AY*40:F$(P,P)="S":P=X+1+Y*40:F$(P,P)="Z"
240 FOR I=0 TO 9
250   P=RAND(1002)+1:IF F$(P,P)<>" " THEN GOTO 250
260   PP(I)=P:F$(P,P)="\A4":NEXT I

270 ZA=PEEK(106)-8:ANF=256*ZA:MOVE 57344,ANF,1024
280 RESTORE 640:FOR I=1 TO 9:READ C:FOR J=0 TO 7:READ B:POKE ANF+C*8+J,B:NEXT J:NEXT I
290 POSITION 14,12:? "press start"
300 WHILE PEEK(53279)<>6:WEND

REM  START 
330 GRAPHICS 18:POKE 559,0:X=21:Y=12:A=0:POKE 708,26:POKE 709,36:POKE 710,200:POKE 711,134
340 COLOR 133:PLOT 7,3:COLOR 134:DRAWTO 12,3:COLOR 135:PLOT 13,3:COLOR 137:DRAWTO 13,8:COLOR 138:PLOT 13,9
350 COLOR 134:DRAWTO 8,9:COLOR 136:PLOT 7,9:COLOR 137:DRAWTO 7,4
360 POSITION 1,1:? #6;"\A4 0":TIME$= "000000":POSITION 14,1:? #6;"00:00":POKE 756,ZA:POKE 559,34

REM  MAIN ROUTINE
390 P=X+Y*40-40
400 POSITION 8,4:? #6;F$(P-41,P-37):POSITION 8,5:? #6;F$(P-1,P+3):POSITION 8,6:? #6;F$(P+39,P+43)
410 POSITION 8,7:? #6;F$(P+79,P+83):POSITION 8,8:? #6;F$(P+119,P+123)
420 PA=P+41:IF F$(PA,PA)="\A4" THEN F$(PA,PA)=" ":A=A+1:POSITION 3,1:? #6;A:COLOR 32:PLOT 10,6
430 IF F$(PA,PA)="Z" AND A=10 THEN GOTO 530

440 T$=TIME$:POSITION 14,1:? #6;T$(3,4);":";T$(5,6)
450 ST=STICK(0)
460 IF ST=7 AND F$(P+42,P+42)<>"x" THEN X=X+1:GOTO 390
470 IF ST=11 AND F$(P+40,P+40)<>"x" THEN X=X-1:GOTO 390
480 IF ST=14 AND F$(P+1,P+1)<>"x" THEN Y=Y-1:GOTO 390
490 IF ST=13 AND F$(P+81,P+81)<>"x" THEN Y=Y+1:GOTO 390
500 GOTO 440

REM  GAME OVER
530 GRAPHICS 0:POKE 559,0:POKE 752,1:POKE 82,0:POKE 710,0:POKE 709,12:POKE 756,ZA:POSITION 0,0
REM " maze _______________ time " with the letters and spaces in inverse"
540 ? "\A0\ED\E1\F9\E5\A0\0E\0E\0E\0E\0E\0E\0E\0E\0E\0E\0E\0E\0E\0E\0E\A0\F4\E9\ED\E5\A0"
550 FOR I=3 TO 6:IF I=5 THEN ? "\ED\E9\Ec\A0";:REM "min " in inverse
560   ? CHR$(ASC(T$(I,I))+128);:NEXT I:? "\F3\E5\E3\A0":REM "sec " in inverse"
570 ? F$(43,961);:COLOR 32:PLOT 39,22:DRAWTO 39,0
580 FOR I=0 TO 9:POKE DPEEK(88)+PP(I)-3,4:NEXT I
590 POKE 559,34
600 WHILE PEEK(53279)<>6:WEND
610 RUN 

630 REM  DATA
640 DATA 5,0,0,15,31,63,63,62,60
650 DATA 6,0,0,255,255,255,255,0,0
660 DATA 7,0,0,240,248,252,252,124,60
670 DATA 8,60,60,62,63,31,15,0,0
680 DATA 9,60,60,60,60,60,60,60,60
690 DATA 10,60,124,252,252,248,240,0,0
700 DATA 56,255,255,255,255,255,255,255,255
710 DATA 4,0,0,24,60,60,24,0,0
720 DATA 120,170,85,170,85,170,85,170,85
