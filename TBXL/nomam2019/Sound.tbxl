0 REM SOUND PROGRAM BY JERRY WHITE 8/18/80
1 GRAPHICS 0:DIM X$(1),BL$(20):BL$="                 "
3 ? :? "PITCH=ANY NUMBER FROM 0 THRU 255.":? "WE WILL MOVE THE PITCH IN LOOP 2."
4 ? :? "L1=OUTER LOOP 1 VOLUME.":? "TYPE ANY NUMBER FROM 0 THRU 15 AT PROMPT FOR M, TO, AND STEP."
5 ? :? "L2=INNER LOOP 2 PITCH.":? "TYPE ANY NUMBER FROM 0 TO 255":? "AT PROMPT FROM, TO, AND STEP."
7 ? :? "HIT return TO BEGIN";:INPUT X$
10 GRAPHICS 0:? :? "sound test"
12 POKE 752,1:C=RND(0)*16:REPEAT=0
14 SETCOLOR 1,C,2:SETCOLOR 2,C,8:SETCOLOR 4,C,2
30 POSITION 2,3:? "TYPE DIST    ";:TRAP 34:INPUT D:TRAP 40000
32 IF D=0 OR D=2 OR D=4 OR D=8 OR D=10 OR D=12 OR D=14 THEN 36
34 POSITION 2,3:? BL$:GOTO 30
36 POSITION 2,5:? "TYPE PITCH    ";:TRAP 40:INPUT P:TRAP 40000
38 IF P<255 THEN 42
40 POSITION 2,5:? BL$:GOTO 36
42 POSITION 2,7:? "TYPE L1 FROM  ";:TRAP 46:INPUT F1:TRAP 40000
44 IF F1<33 THEN 48
46 POSITION 2,7:? BL$:GOTO 36
48 POSITION 2,9:? "TYPE L1 TO    ";:TRAP 52:INPUT T1:TRAP 4000
50 IF T1<33 THEN 54
52 POSITION 2,9:? BL$:GOTO 48
54 POSITION 2,11:? "TYPE L1 STEP ";:TRAP 58:INPUT S1:TRAP 40000
56 IF S1<33 THEN 60
58 POSITION 2,11:? BL$:GOTO 54
60 POSITION 2,13:? "TYPEL2 FROM ";:TRAP 64:INPUT F2:TRAP 40000
62 IF F2<256 THEN 70
64 POSITION 2,13:? BL$:GOTO 60
70 POSITION 2,15:? "TYPE L2 TO    ";:TRAP 74:INPUT T2:TRAP 40000
72 IF T2<256 THEN 80
74 POSITION 2,15:? BL$:GOTO 70
80 POSITION 2,17:? "TYPE L2 STEP ";:TRAP 84:INPUT S2:TRAP 40000
82 IF S2<256 THEN 100
84 POSITION 2,17:? BL$:GOTO 80
100 IF REPEAT>0 THEN GOSUB 5000:GOTO 400
120 GOSUB 5000:SOUND 0,0,0,0:? CHR$(125)
130 ? :? "  your sound subroutine: "
140 ? :? "100 FOR L1=";F1;" TO ";T1;" STEP ";S1
160 ? "110 FOR L2=";F2;" TO ";T2;" STEP ";S2
180 ? "120 SOUND 0,";P;"-L2,";D;",L1"
200 ? "130 NEXT L2":? "140 NEXT L1":? "150 RETURN"
280 FOR DELAY=1 TO 500:NEXT DELAY
300 FOR TIME=1 TO 5:POSITION 2,20:? "  HIT START TO RESTART":? "  HIT OPTION TO REPEAT "
320 FOR DELAY=1 TO 10:NEXT DELAY
340 POSITION 2,20:? "  HIT start TO RESTART":? "  HIT option TO REPEAT "
360 FOR DELAY=1 TO 10:NEXT DELAY
380 NEXT TIME
400 SOUND 0,0,0,0
410 IF PEEK(53279)=6 THEN 10
420 IF PEEK(53279)=3 THEN 500
440 GOTO 410
500 REPEAT=REPEAT+1:GOTO 100
5000 FOR L1=F1 TO T1 STEP S1
5100 FOR L2=F2 TO T2 STEP S2
5200 TRAP 6000:SOUND 0,P-L2,D,L1:TRAP 40000
5300 NEXT L2:NEXT L1:RETURN 
6000 ? :? "    invalid sound, try again    ":SOUND 0,0,0,0
6100 FOR DELAY=1 TO 250:NEXT DELAY
6110 GOTO 10
