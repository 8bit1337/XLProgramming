REM Note: the following is taken from the old listing for Wizard's Revenge which was printed on a roll of four-inch-wide paper from the old Atari printer. 
REM Some of the listing is very hard to read because the ink has faded, but I think the following is accurate.

REM BOTH THE PROGRAM LISTED BELOW
REM AND THE SET OF FILES WHICH ARE
REM LISTED AFTER IT ARE COPYRIGHTED
REM 1981 BY MAXWELL E. MANOWSKI

0 GOSUB 20000
20 PRINT "\7D" : PRINT
21 PRINT "          WIZARD'S REVENGE"
22 PRINT "          COPYRIGHT 1981"
23 PRINT "          BY MAX MANOWSKI"
24 PRINT
30 DIM INVENTORY$(20)
31 DIM NODE$(8)
32 DIM FILE$(10)
33 DIM PRINT$(50)
34 DIM INPUT$(50)
35 DIM TEMP$(52)
36 DIM CONDITION$(64)
50 HELP=0
51 LIGHT=0
52 CONMAX=6
53 CON=6
100 FOR N=1 TO 64
110   CONDITION$(N,N)="N"
120 NEXT N
200 NODE$="INIT"
300 FOR N=1 TO 20
310   INVENTORY$(N,N)="N"
320 NEXT N
340 EOF=0
390 REM
900 PRINT "    \D9\CF\D5\A0\C8\C1\D6\C5\A0\CD\C1\C4\C5\A0\C1\A0\D0\CF\D7\C5\D2\C6\D5\CC\A0\A0\A0\A0"
901 PRINT "    \D7\C9\DA\C1\D2\C4\A0\D6\C5\D2\D9\A0\C1\CE\C7\D2\D9\AE\A0\C8\C5\A0\C8\C1\D3\A0\A0\A0"
902 PRINT "    \D0\D5\D4\A0\D9\CF\D5\A0\C9\CE\D4\CF\A0\C1\A0\D3\D4\D2\C1\CE\C7\C5\A0\CC\C1\CE\C4\A0"
903 PRINT "    \D7\C9\D4\C8\A0\CE\CF\D4\C8\C9\CE\C7\A0\C2\D5\D4\A0\D4\C8\C5\A0\C3\CC\CF\D4\C8\C5\D3"
904 PRINT "    \D4\C8\C1\D4\A0\D9\CF\D5\A0\C1\D2\C5\A0\D7\C5\C1\D2\C9\CE\C7\AE\A0\A0\A0\A0\A0\A0\A0"
910 PRINT
911 PRINT "YOUR OBJECT IS TO GET OUT OF THIS"
912 PRINT "STRANGE LAND."
915 PRINT "YOU MAY TRY ANYTHING YOU WANT"
920 PRINT "TO GET OUT OF THIS MESS. IF"
925 PRINT "YOU CAN'T THINK OF ANYTHING ELSE"
930 PRINT "TO TRY, THEN JUST TYPE THE WORD HELP."
935 PRINT "BE WARNED, USING 'HELP' CAN"
940 PRINT "BE DANGEROUS!!!"
950 PRINT "OTHER USEFUL WORDS ARE INVENTORY"
960 PRINT "AND LOOK. \D0\D2\C5\D3\D3\A0\D2\C5\D4\D5\D2\CE\A0\D4\CF\A0\D3\D4\C1\D2\D4"
970 INPUT INPUT$
1000 REM PRINT DESCRIPTION FIRST /////
1010 GOSUB 3000
2000 REM GET ACTIONS /////
2005 CLOSE #1
2007 PRINT "\D7\C8\C1\D4\A0\C1\D2\C5\A0\D9\CF\D5\A0\C7\CF\C9\CE\C7\A0\D4\CF\A0\C4\CF\BF"
2010 INPUT INPUT$
2015 PRINT
2020 IF LEN(INPUT$)>36 THEN PRINT "SAY IT WITH FEWER WORDS":GOTO 2007
2030 IF LEN(INPUT$)=0 THEN PRINT "YOU MUST TYPE AN INSTRUCTION.":GOTO 2010
2035 IF LEN(INPUT$)=1 THEN PRINT "I DO NOT ACCEPT ABBREVIATIONS.":GOTO 2010
2043 TEMP$=" "
2046 TEMP$(2)=INPUT$
2050 TEMP$(LEN(TEMP$)+1)=" "
2060 LET INPUT$=TEMP$:IF LEN(INPUT$)<5 THEN 2064
2061 PRINT "\1C\1C\9C":FOR N=1 TO LEN(INPUT$)-4
2062 IF INPUT$(N,N+4)=" THE " THEN LET INPUT$(N,N+4)="     "
2063 NEXT N
2064 FOR N=1 TO LEN(INPUT$)-2:IF INPUT$(N,N+2)=" A " THEN LET INPUT$(N,N+2)="   "
2065 NEXT N
2066 FOR N=1 TO LEN(INPUT$)
2067   TEMP$=INPUT$(N,N)
2070   IF TEMP$="," OR TEMP$="." OR TEMP$="!" THEN LET INPUT$(N,N)=" "
2080   IF TEMP$<>"?" THEN 2120
2090   PRINT "I DON'T ANSWER QUESTIONS"
2100   POP
2110   GOTO 2010
2120 NEXT N
2130 N=1
2135 IF N=LEN(INPUT$) THEN 2160
2140 IF INPUT$(N,N+1)="  " THEN LET INPUT$(N)=INPUT$(N+1):GOTO 2135
2150 N=N+1:GOTO 2135
2160 PRINT INPUT$
2165 PRINT
2166 IF LEN(INPUT$)<7 THEN 2210
2170 IF INPUT$(1,6)=" WHAT " OR INPUT$(1,7)=" WHERE " OR INPUT$(1,5)=" HOW " OR INPUT$(1,5)=" WHY " THEN 2090
2180 FOR N=1 TO LEN(INPUT$)-4:IF INPUT$(N,N+4)=" AND " THEN 2200
2190 NEXT N:GOTO 2210
2200 PRINT "YOU ARE TRYING TO DO TOO MUCH WITH":PRINT "ONE INSTRUCTION.":GOTO 2010
2210 REM
2500 REM TAKE ACTION /////
2510 TRAP 32750
2520 FILE$="D:"
2530 FILE$(3)=NODE$
2540 OPEN #1,4,0,FILE$
2550 TRAP 32750
2560 GOSUB 4400
2570 IF FOUND THEN 2640
2580 CLOSE #1
2590 OPEN #1,4,0,"D:DEFAULT"
2600 GOSUB 4400
2610 IF FOUND THEN 2640
2620 GOSUB 4600
2630 GOTO 2660
2640 REM REM FOUND A MATCH /////
2650 GOSUB 4800
2660 CLOSE #1
2670 TRAP 32750
2680 GOTO 2000
3000 REM PRINT DESCRIPTION /////
3005 IF NODE$="END" THEN 9200
3007 CLOSE #1
3010 TRAP 3090
3020 FILE$="D:"
3030 FILE$(3)=NODE$
3040 OPEN #1,4,0,FILE$
3050 GOSUB 4815
3060 RETURN
3090 TRAP 32750
3100 CLOSE #1
3110 RETURN
4400 REM FIND A MATCH /////
4410 FOUND=0
4420 GOSUB 5400
4430 IF EOF THEN CLOSE #1:GOTO 4590
4440 INPUT #1,TEMP$
4445 IF TEMP$="**" THEN CLOSE #1:RETURN
4450 IF TEMP$="*" THEN 4420
4455 IF TEMP$="/ANY/" THEN 4580
4460 LET PRINT$=" ":LET PRINT$(2)=TEMP$
4465 TEMP$=PRINT$:TEMP$(LEN(TEMP$)+1)=" "
4470 IF LEN(TEMP$)>LEN(INPUT$) THEN 4440
4480 FOR N=1 TO LEN(INPUT$)-LEN(TEMP$)+1
4490 IF INPUT$(N,N+LEN(TEMP$)-1)=TEMP$ THEN GOTO 4580
4500 NEXT N
4510 GOTO 4440
4580 FOUND=1
4581 PRINT "\1C";
4582 FOR NN=N+2 TO N+LEN(TEMP$)-1
4583   POKE 85,NN:PRINT "\1B\1C";:NEXT NN
4584 PRINT
4590 TRAP 32750:RETURN
4600 REM RANDOM RESPONSE /////
4610 GOTO INT(RND(0)*5)*10+4620
4620 PRINT "HUH?":RETURN
4630 PRINT "WHAT?":RETURN
4640 PRINT "I DON'T UNDERSTAND":RETURN
4650 PRINT "YOU CAN'T DO THAT":RETURN
4660 GOSUB 4600
4670 PRINT "WOULD YOU PUT THAT ANOTHER WAY"
4680 RETURN
4800 REM DO ACTIONS /////
4810 GOSUB 5400
4815 IF EOF THEN EOF=0:CLOSE #1:RETURN
4820 INPUT #1,INPUT$
4825 IF INPUT$="**" THEN EOF=1:GOTO 4815
4830 IF INPUT$="*" THEN RETURN
4840 TEMP$=INPUT$(1,1)
4845 IF LEN(INPUT$)=1 THEN LET INPUT$="":GOTO 4860
4850 LET INPUT$=INPUT$(2)
4855 TRAP 32750
4860 IF TEMP$="P" THEN PRINT INPUT$:GOTO 4820
4861 IF TEMP$="A" THEN LIGHT=1:LET INPUT$=""
4862 IF TEMP$="B" THEN LIGHT=0:LET INPUT$=""
4863 IF TEMP$="E" THEN CON=CONMAX:LET INPUT$=""
4864 IF TEMP$="J" THEN GOSUB 7400:CONDITION=INVENTORY$(N,N)="Y":LET INPUT$=""
4870 IF TEMP$="G" THEN NODE$=INPUT$:CLOSE #1:GOTO 3000
4871 IF TEMP$="X" THEN 9600
4872 IF TEMP$="H" THEN 9800
4873 IF TEMP$="U" THEN IF LIGHT=0 THEN 4840
4874 IF TEMP$="V" THEN IF LIGHT=1 THEN 4840
4880 IF TEMP$="Q" THEN GOTO 5600
4890 IF TEMP$="D" THEN GOTO 5800
4900 IF TEMP$="I" THEN GOSUB 6000
4910 IF TEMP$="M" THEN GOSUB 6200:GOTO 2000
4920 IF TEMP$="T" THEN GOSUB 6400
4930 IF TEMP$="W" THEN FOR N=1 TO VAL(INPUT$):NEXT N:GOTO 4820
4940 IF TEMP$="R" THEN GOSUB 7200
4941 IF TEMP$="C" THEN 9300
4942 IF TEMP$="0" THEN 9400
4943 IF TEMP$="1" THEN 9500
4950 IF TEMP$="Y" THEN IF CONDITION THEN 4840
4960 IF TEMP$="N" THEN IF  NOT CONDITION THEN 4840
4970 IF TEMP$="L" THEN GOSUB 7400:TEMP$="":IF FOUND THEN INVENTORY$(N,N)="N"
4971 IF TEMP$="K" THEN GOSUB 7400:TEMP$="":IF FOUND THEN INVENTORY$(N,N)="Y"
4980 IF TEMP$="S" THEN GOSUB 7800
4981 IF TEMP$="F" THEN 9900
4982 IF TEMP$="Z" THEN GOTO 6900
4990 GOTO 4820
5000 RUN 
5400 REM SKIP A BLOCK /////
5402 EOF=0
5405 TRAP 5440
5410 INPUT #1,TEMP$
5420 IF TEMP$="*" THEN RETURN
5430 GOTO 5410
5440 TRAP 32750
5450 EOF=1
5460 CLOSE #1
5470 CLOSE #2
5480 RETURN
5600 REM QUIT /////
5610 CLOSE #1
5620 CLOSE #2
5630 TRAP 32750
5640 PRINT "DO YOU WANT TO PLAY AGAIN";
5650 INPUT INPUT$
5655 IF LEN(INPUT$)=0 THEN 5640
5660 IF INPUT$(1,1)="Y" THEN GOSUB 32750:RUN
5670 PRINT "\7D":END
5800 REM DIE /////
5810 GRAPHICS 2:GOSUB 32740
5820 POSITION 9,4
5830 PRINT #6;"you"
5840 POSITION 9,6
5850 PRINT #6;"are"
5860 POSITION 7,8
5870 PRINT #6;"\e4 \e5 \e1 \e4"
5880 FOR N=1 TO 500:NEXT N
5890 GOTO 5600
6000 REM PRINT INVENTORY /////
6010 FLAG=0
6015 N=0
6020 TRAP 6090
6030 RESTORE
6040 PRINT "YOU HAVE:"
6050 READ TEMP$
6060 N=N+1
6070 IF INVENTORY$(N,N)="Y" THEN FLAG=1:PRINT TEMP$
6080 GOTO 6050
6090 IF LIGHT THEN PRINT "TORCH":FLAG=1
6095 IF  NOT FLAG THEN PRINT "NOTHING"
6100 TRAP 32750
6105 TEMP$=""
6110 RETURN
6200 REM FIGHT MONSTER /////
6205 TRAP 32750
6210 N=VAL(INPUT$)
6213 TEMP$=""
6215 IF N<(100*RND(0)) THEN RETURN
6217 MONSIZE=INT(RND(0)*3)
6219 PRINT "THERE IS A ";
6220 IF MONSIZE=0 THEN PRINT "LITTLE ";
6221 IF MONSIZE=1 THEN PRINT "BIG ";
6222 IF MONSIZE=2 THEN PRINT "HUGE ";
6223 PRINT "MONSTER HERE!"
6224 PRINT "YOU CAN SEE THAT \D9\CF\D5\A0\CD\D5\D3\D4\A0\CB\C9\CC\CC\A0\C9\D4"
6225 PRINT "OR IT WILL \CB\C9\CC\CC\A0\D9\CF\D5"
6227 HTK=CON:MHTK=3+3*MONSIZE
6230 PRINT:PRINT "  \D7\C8\C1\D4\A0\C1\D2\C5\A0\D9\CF\D5\A0\C7\CF\C9\CE\C7\A0\D4\CF\A0\C4\CF\BF"
6233 HIT=0:DAM=0
6235 INPUT INPUT$
6236 PRINT
6240 TEMP$=" ":TEMP$(2)=INPUT$
6245 TEMP$(LEN(TEMP$)+1)=" "
6250 LET INPUT$=TEMP$
6255 CLOSE #1
6260 OPEN #1,4,0,"D:MONSTER"
6265 GOSUB 4400
6270 IF  NOT FOUND THEN PRINT "THAT HAS NO EFFECT ON IT.":GOTO 6325
6275 GOSUB 5400
6280 IF EOF THEN 6325
6285 INPUT #1,INPUT$
6290 IF INPUT$="*" THEN 6325
6300 TEMP$=INPUT$(1,1)
6302 IF LEN(INPUT$)<2 THEN LET INPUT$="":GOTO 6310
6305 LET INPUT$=INPUT$(2)
6310 IF TEMP$="W" THEN GOSUB 7200:IF  NOT FOUND THEN GOTO 6325
6311 IF TEMP$="P" THEN PRINT INPUT$
6312 IF TEMP$="L" THEN GOSUB 7400:TEMP$="":IF FOUND THEN INVENTORY$(N,N)="N"
6313 IF TEMP$="X" THEN LET INPUT$="75":GOTO 6800
6314 IF TEMP$="M" THEN N=VAL(INPUT$):MONSIZE=N:MHTK=3+3*MONSIZE:GOTO 6285
6315 IF TEMP$="H" THEN HIT=HIT+VAL(INPUT$)
6316 IF TEMP$="0" THEN IF MONSIZE=0 THEN 6300
6317 IF TEMP$="1" THEN IF MONSIZE=1 THEN 6300
6318 IF TEMP$="2" THEN IF MONSIZE=2 THEN 6300
6319 IF TEMP$="T" THEN IF LIGHT=1 THEN GOTO 6300
6320 IF TEMP$="T" THEN PRINT "BUT YOU DON'T HAVE A TORCH"
6321 IF TEMP$="D" THEN DAM=DAM+VAL(INPUT$)
6322 GOTO 6285
6325 REM TAKE BATTLE ACTIONS /////
6330 TRAP 32750
6335 CLOSE #1
6340 IF (100*RND(0))<HIT THEN PRINT "YOU WOUND THE MONSTER":MHTK=MHTK-DAM
6345 IF MHTK<1 THEN PRINT "AND YOU HAVE KILLED HIM!":LET INPUT$="75":GOTO 6800
6350 IF RND(0)>(0.5+0.1*MONSIZE) THEN 6355
6352 PRINT "THE MONSTER BITES YOU."
6353 N=5*RND(0)-MONSIZE:IF N<0 THEN N=0.05
6354 HTK=HTK-N
6355 IF HTK<0 THEN FOR W=1 TO 200:NEXT W:GOTO 5800
6370 PRINT "YOU ARE ";INT(HTK/12*100);"% ALIVE."
6380 GOTO 6230
6400 REM FOUND TREASURE /////
6410 N=VAL(INPUT$)
6415 TEMP$=""
6420 IF N<(100*RND(0)) THEN RETURN
6440 GOSUB 7000
6445 NN=N:WWW=N
6450 N=INT(N*RND(0))+1
6455 NN=NN-1:IF NN=0 THEN RETURN 
6460 IF INVENTORY$(N,N)="N" THEN 6470
6463 N=N+1:IF N>WWW THEN N=1
6466 GOTO 6455
6470 RESTORE
6480 FOR NN=1 TO N
6490 READ TEMP$
6500 NEXT NN
6510 PRINT "YOU FIND AND KEEP A ";
6520 INVENTORY$(N,N)="Y"
6530 PRINT TEMP$
6540 TEMP$="":RETURN
6600 REM DATA FOR INVENTORY /////
6610 DATA SWORD
6615 DATA KEY
6620 DATA ROPE
6625 DATA POTION
6630 DATA WORM
6635 DATA FLUTE
6640 DATA RED GEM
6645 DATA GOLD COIN
6650 DATA SILVER BUTTON
6655 DATA RAG
6660 DATA PEARL
6665 DATA BOOK
6670 DATA SMALL RUBBER BALL
6800 PRINT "YOU REST A BIT."
6805 CON=CON-(CON-HTK)*RND(0)
6810 PRINT "YOU ARE ";INT(CON/12*100);"% ALIVE."
6820 GOTO 6400
6900 REM REST /////
6910 CON=CON+0.1
6912 TEMP$=""
6915 IF RND(0)<0.2 THEN GOSUB 6217:GOTO 2000
6920 GOTO 9850
7000 REM NUMBER IN INVENTORY (=N) /////
7010 TRAP 7070
7020 N=0
7030 RESTORE
7040 READ TEMP$
7050 N=N+1
7060 GOTO 7040
7070 TRAP 32750
7080 RETURN
7200 REM REQUIRE ITEM /////
7210 TEMP$=""
7220 CONDITION=0
7230 GOSUB 7400
7240 IF  NOT FOUND THEN RETURN
7245 IF INVENTORY$(N,N)="N" THEN PRINT "BUT YOU DON'T HAVE A ";INPUT$:FOUND=0:GOTO 7260
7250 CONDITION=1
7260 TEMP$=""
7270 RETURN
7400 REM FIND ITEM AND ITEM NUMBER /////
7410 N=0
7420 FOUND=0
7430 TRAP 7490
7440 RESTORE
7450 READ TEMP$
7460 N=N+1
7470 IF TEMP$=INPUT$ THEN FOUND=1:GOTO 7490
7480 GOTO 7450
7490 TRAP 32750:RETURN
7800 REM MAKE SOUND /////
7805 LET SND=VAL(INPUT$)
7810 IF SND<>1 THEN 7820
7813 REM EXPLOSION /////
7814 FOR N=200 TO 0 STEP -10
7815 SOUND 0,100,0,N/13.333
7816 SOUND 1,30,4,N/13.333
7817 SOUND 2,230-N,8,N/13.333
7818 FOR NN=202 TO N STEP -10
7819 NEXT NN:NEXT N
7820 IF SND<>2 THEN 7830:REM FALL ///
7821 FOR N=0 TO 200
7822 SOUND 0,N,10,15
7823 NEXT N
7824 SOUND 0,0,0,0
7830 REM CHIRP UP /////
7831 IF SND<>3 THEN GOTO 7840
7832 FOR N=30 TO 15 STEP -1
7833   SOUND 0,N,10,15
7834 NEXT N
7835 SOUND 0,0,0,0
7840 REM CHIRP DOWN /////
7841 IF SND<>4 THEN 7850
7842 FOR N=15 TO 45
7843 SOUND 0,N,14,8
7844 NEXT N
7845 FOR N=1 TO 30:NEXT N
7846 SOUND 0,0,0,0
7850 REM POPPING /////
7851 IF SND<>5 THEN 7990
7854 FOR N=1 TO 30
7855 SOUND 0,100,0,8
7856 SOUND 1,30,4,8
7857 SOUND 2,200,8,8
7859 SOUND 0,0,0,0
7860 SOUND 1,0,0,0
7861 SOUND 2,0,0,0
7862 FOR NN=1 TO 1+RND(0)*5:NEXT NN
7863 NEXT N
7990 TEMP$="":RETURN
9200 GRAPHICS 2:GOSUB 32740
9210 POSITION 3,2:PRINT #6;"\43\6F\CE\E7\52\61\D4\F5\4C\61\D4\E9\4F\6E\D3"
9220 POSITION 6,4:PRINT #6;"you have"
9230 POSITION 5,5:PRINT #6;"made it \EF\F5\F4"
9290 FOR Y=1 TO 300:NEXT Y:RUN 
9300 REM CHECK CONDITION$ /////
9310 N=VAL(INPUT$)
9320 IF CONDITION$(N,N)="Y" THEN CONDITION=1:GOTO 4820
9330 CONDITION=0:GOTO 4820
9400 REM CLEAR CONDITION$ /////
9410 N=VAL(INPUT$)
9420 CONDITION$(N,N)="N"
9430 GOTO 4820
9500 REM SET CONDITION$ /////
9510 N=VAL(INPUT$)
9520 CONDITION$(N,N)="Y"
9530 GOTO 4820
9600 REM WORK LOOK /////
9610 GOSUB 3000
9615 POP
9620 GOTO 2000
9800 REM WORK ADD TO CONMAX /////
9810 N=VAL(INPUT$)
9820 CONMAX=CONMAX+N
9830 IF CONMAX>12 THEN CONMAX=12
9840 IF CONMAX<=0 THEN FOR N=1 TO 200:NEXT N:GOTO 5800
9850 IF CON>CONMAX THEN CON=CONMAX
9855 PRINT "YOU ARE ";INT(CON/12*100);"% ALIVE."
9860 GOTO 4820
9900 REM WORK HELP /////
9901 CLOSE #1
9902 HELP=HELP+1
9904 IF HELP<4 THEN 9910
9906 IF RND(0)*10<HELP THEN LET INPUT$="50":GOSUB 6200
9910 N=0
9915 PRINT "YOU MIGHT TRY ONE OF THESE:"
9920 FILE$="D:"
9925 FILE$(3)=NODE$
9927 CLOSE #1
9930 OPEN #1,4,0,FILE$
9940 GOSUB 5400
9950 IF EOF THEN 10000
9955 INPUT #1,INPUT$
9960 IF INPUT$="*" THEN 9940
9970 IF INPUT$="**" THEN 10000
9972 IF INPUT$="/ANY/" THEN 9955
9975 IF RND(0)<0.25 THEN PRINT INPUT$:N=N+1:IF N=3 THEN CLOSE #1:GOTO 2010
9980 GOTO 9955
10000 IF FILE$="D:DEFAULT" THEN 10100
10005 CLOSE #1
10010 FILE$="D:DEFAULT"
10020 GOTO 9930
10100 CLOSE #1
10110 IF N<2 THEN GOTO 9920
10120 GOTO 2010
20000 GOSUB 32740:POKE 580,1:TRAP 32750:POKE 4226,105:POKE 4229,1:RETURN 
32710 GRAPHICS 0:GOSUB 32740:RETURN 
32720 POKE 709,10:GRAPHICS 0:POKE 752,0:RETURN 
32740 Z=PEEK(16):IF Z>=128 THEN Z=Z-128
32741 POKE 16,Z:POKE 53774,Z:RETURN 
32750 PRINT "ERROR ";PEEK(195);".  PROGRAM ABORTS."
32751 FOR BA=1 TO 400:NEXT BA:GOSUB 32766:POKE 580,1:BB=USR(58487)
32760 BB=PEEK(130)+256*PEEK(131)
32761 G=PEEK(132)+256*PEEK(133)-1
32762 FOR BA=BB TO G:POKE BA,155:NEXT BA:STOP 
32766 POKE (PEEK(138)+256*PEEK(139)+2),0:RETURN 